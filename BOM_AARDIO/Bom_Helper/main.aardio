import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
mainForm = win.form(text="SAP_Drawing_Browser";right=1040;bottom=642;bgcolor=15793151;border="none")
mainForm.add(
caption={cls="bkplus";text="Domisoft";left=64;top=12;right=160;bottom=30;align="left";color=15780518;dl=1;dt=1;font=LOGFONT(h=-16);z=5};
custom={cls="custom";left=0;top=40;right=1040;bottom=643;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;z=1};
logo={cls="bkplus";text='\uF262';left=0;top=0;right=56;bottom=40;color=15780518;dl=1;dt=1;font=LOGFONT(h=-29;name='FontAwesome');z=4};
navFirst={cls="plus";text="主页";left=166;top=5;right=260;bottom=40;align="left";color=16777215;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome');padding={left=12;top=4}};iconText='\uF00A';notify=1;paddingLeft=1;paddingRight=1;paddingTop=3;textPadding={left=39;bottom=1};x=0.5;y=0.2;z=3};
navHelp={cls="plus";text="帮助";left=462;top=5;right=556;bottom=40;align="left";color=16777215;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome');padding={left=12;top=4}};iconText='\uF06A';notify=1;paddingLeft=1;paddingRight=1;paddingTop=3;textPadding={left=39;bottom=1};x=0.5;y=0.2;z=7};
navHotkey={cls="plus";text="热键";left=265;top=5;right=359;bottom=40;align="left";color=16777215;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome');padding={left=12;top=4}};iconText='\uF11C ';notify=1;paddingLeft=1;paddingRight=1;paddingTop=3;textPadding={left=39;bottom=1};x=0.5;y=0.2;z=8};
navSetting={cls="plus";text="设置";left=363;top=5;right=457;bottom=40;align="left";color=16777215;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome');padding={left=12;top=4}};iconText='\uF0AD';notify=1;paddingLeft=1;paddingRight=1;paddingTop=3;textPadding={left=39;bottom=1};x=0.5;y=0.2;z=6};
titleBar={cls="bkplus";left=0;top=0;right=1042;bottom=41;bgcolor=14977280;dl=1;dr=1;dt=1;z=2}
)
/*}}*/

/*只允许一个实例{{*/
import win.ui.atom;
var atom,hwnd = mainForm.atom("domisoft.aardio.helper");
if(!atom){ 
	win.showForeground(hwnd);
	win.quitMessage();	
	return;
}
/*}}*/
import win.ui.simpleWindow;
win.ui.simpleWindow( mainForm );

import win.ui.tabs;
var tbs = win.ui.tabs( 
	mainForm.navFirst,
	mainForm.navHotkey,
	mainForm.navSetting,
	mainForm.navHelp
);
	
tbs.skin({
	foreground={
		active=0xFFFFFFFF;
		default=0x00FFFFFF;
		hover=0x38FFFFFF
	};
	color={
		default=0xFFFFFFFF; 
	};
	checked={
		foreground={default=0xFFFFFFFF;}; 
		color={default=0xFF42A875;};
	}
})
	
tbs.loadForm(1,"\dlg\first.aardio" );  
tbs.loadForm(2,"\dlg\hotkey.aardio" );  
tbs.loadForm(3,"\dlg\setting.aardio" ); 
tbs.loadForm(4,"\dlg\help.aardio" ); 
tbs.selIndex = 1; 

/*托盘图标{{*/
import win.util.tray;
mainForm.onMinimize = function(lParam){
	mainForm.tray = win.util.tray(mainForm)  
	mainForm.modifyStyleEx(0x40000/*_WS_EX_APPWINDOW*/,0x80/*_WS_EX_TOOLWINDOW*/)
	mainForm.show(false); 
	return true; 
}

mainForm.onClose = function(hwnd,message,wParam,lParam){
	if(!_STUDIO_INVOKED){
		mainForm.onMinimize();
		return true;    	
	} 
}

mainForm.onDestroy = function(){
	if(mainForm.tray){
		mainForm.tray.delete()
	}
}

import win.ui.menu;

mainForm.wndproc = {
	[0xACCF/*_WM_TRAYMESSAGE*/ ] = function(hwnd,message,wParam,lParam){
		if( lParam = 0x205/*_WM_RBUTTONUP*/ ){
			win.setForeground(mainForm.hwnd)
			mainForm.popmenu = win.ui.popmenu(mainForm);//创建弹出菜单
			mainForm.popmenu.add('打开',function(id){
				//tbs.selIndex = 4;
				mainForm.show();
			});
			mainForm.popmenu.add();			
			mainForm.popmenu.add('退出',function(id){
				win.quitMessage();
			});			
			var pt = ::POINT();
			::User32.GetCursorPos(pt);
			mainForm.popmenu.popup(pt.x,pt.y,true)
		}
		elseif( lParam = 0x202/*_WM_LBUTTONUP*/ ) {
			mainForm.show();
			win.setForeground(mainForm.hwnd);
		} 	
	}
}
/*}}*/


mainForm.enableDpiScaling();
if(_ARGV.tray){ 
	mainForm.modifyStyleEx(0x40000/*_WS_EX_APPWINDOW*/,0x80/*_WS_EX_TOOLWINDOW*/)	
	mainForm.setTimeout( 
		function(){
			mainForm.tray = win.util.tray(mainForm)  
		},3000
	); 	
}
else {
	mainForm.show();
}
mainForm.onActivate = function(state,hwndOther,minimized){
	if(state){
		if (tbs.selIndex==1) tbs.selForm.updateExcelStatus();
	}
}
return win.loopMessage(); 
