import win.ui;
_IMPORTURL["reportRl"] = "https://github.com/drunkenOstrich/reportRl/releases/latest/download/reportRl.tar.lzma"
import reportRl;
import reportRl.path;
import reportRl.printView;
import reportRl.displayView;
/*DSG{{*/
mainForm = win.form(text="各种格式报表演示";right=814;bottom=717;maxmize=1)
mainForm.add(
btnCustomPreview={cls="button";text="自定义打印预览...";left=529;top=681;right=678;bottom=709;db=1;z=9};
btnDesign={cls="button";text="设计模版...";left=119;top=613;right=223;bottom=641;dl=1;dt=1;z=6};
btnDisplay={cls="button";text="查询显示...";left=3;top=613;right=107;bottom=641;dl=1;dt=1;z=5};
btnStandardPreview={cls="button";text="标准打印预览...";left=380;top=681;right=529;bottom=709;db=1;z=8};
custom1={cls="custom";text="custom";left=236;top=13;right=803;bottom=670;db=1;dl=1;dr=1;dt=1;hide=1;z=10};
edtMemo={cls="edit";left=3;top=643;right=223;bottom=713;autohscroll=false;db=1;dl=1;dt=1;edge=1;multiline=1;readonly=1;vscroll=1;z=7};
groupbox={cls="groupbox";text="报表展现方式";left=3;top=5;right=223;bottom=46;dl=1;dt=1;edge=1;z=1};
rdoDisplay={cls="radiobutton";text="查询显示";left=126;top=20;right=196;bottom=39;dl=1;dt=1;z=3};
rdoPreview={cls="radiobutton";text="预览显示";left=33;top=20;right=103;bottom=39;checked=1;dl=1;dt=1;z=2};
static2={cls="static";text="static2";left=236;top=13;right=803;bottom=670;clip=1;db=1;dl=1;dr=1;dt=1;hide=1;notify=1;z=11};
treeview={cls="treeview";left=3;top=48;right=223;bottom=610;asel=false;bgcolor=16777215;dl=1;dt=1;edge=1;fullRow=1;singleExpand=false;vscroll=1;z=4}
)
/*}}*/

var report,err = reportRl();//创建锐浪报表主对象
if(!report){
	mainForm.msgbox(err)
}

var print,err = reportRl.printView(mainForm.custom1);//创建打印视图嵌入控件 - 预览显示
if(!print){
	mainForm.msgbox(err)
}

var display,err = reportRl.displayView(mainForm.static2);//创建显示视图嵌入控件 - 查询显示
if(!display){
	mainForm.msgbox(err)
}

//给控件关联报表主对象
print.Report = report;
display.Report = report;

mainForm.rdoDisplay.oncommand = function(id,event){
	//查询显示模式 单选按钮被点击
	mainForm.custom1.show(false);
	mainForm.static2.show(true);
	display.Start();
}

mainForm.rdoPreview.oncommand = function(id,event){
	//预览显示模式 单选按钮被点击
	mainForm.custom1.show(true);
	mainForm.static2.show(false);
	print.Start();
}

//初始化树形框函数
var initTreeview = function(){
	var rootItem = mainForm.treeview.insertItem("Grid++Report");
	var item = null;
	for line in io.lines(reportRl.path.templatePath() ++ "Program\ReportList.txt") { 
		if(line[[1]] = '>'){
			item = mainForm.treeview.insertItem(string.fromto(line,936,0),rootItem);
		}
		else {
			mainForm.treeview.insertItem(string.fromto(line,936,0),item);
		}
	}
	for hItem in mainForm.treeview.each(rootItem){
		mainForm.treeview.expand(hItem);
	}
	mainForm.treeview.expand(rootItem);
}

var templateFile = "";//模版文件名

//树形框的消息通知
mainForm.treeview.onnotify = function(id,code,ptr){

	if(code == 0xFFFFFE3D/*_TVN_SELCHANGEDW*/){//treeview项目改变
		var itemText = null;//选中项文本
		var btnEnabled = false;//按钮是否禁用
		
		var nm = mainForm.treeview.getNotifyMessage(code,ptr);
		if(nm){
			itemText = mainForm.treeview.getItemText(nm.itemNew.hItem);
			if(itemText[[1]]=='>' || itemText = "Grid++Report"){
				btnEnabled = true;
				mainForm.edtMemo.text = "";
			}
			else {
				print.Stop();
				display.Stop();
				//从对应文件中载入报表模板数据
				templateFile = reportRl.path.templatePath() ++ itemText ++ ".grf";
				report.LoadFromFile(templateFile)
				if(report.DetailGrid){
					report.DetailGrid.Recordset.ConnectionString = "Provider=Microsoft.Jet.OLEDB.4.0;User ID=Admin;Data Source=" + reportRl.path.dataPath() + "NorthWind.mdb;";
					report.ConnectionString = "";
				}
				else {
					report.ConnectionString = "Provider=Microsoft.Jet.OLEDB.4.0;User ID=Admin;Data Source=" + reportRl.path.dataPath() + "NorthWind.mdb;";
				}
				mainForm.text = "各种格式报表演示 - " ++ templateFile;
				mainForm.edtMemo.text = report.Description;
				if(mainForm.rdoPreview.checked){
					print.Start();
				}
				else {
					display.Start();
				}
			}
			mainForm.btnDesign.disabled = btnEnabled;
			mainForm.btnDisplay.disabled = btnEnabled;
			mainForm.btnCustomPreview.disabled = btnEnabled;
			mainForm.btnStandardPreview.disabled = btnEnabled;
		}
	}
}

mainForm.btnDisplay.oncommand = function(id,event){
	//查询显示...
	var displayViewForm,wb = mainForm.loadForm("\res\displayViewForm.aardio",report);
	displayViewForm.show();
}

//设计模版被点击
mainForm.btnDesign.oncommand = function(id,event){
	var designForm,wb = mainForm.loadForm("\res\designForm.aardio",{templateFile;report;print});
	designForm.doModal()
}

mainForm.btnStandardPreview.oncommand = function(id,event){
	//标准打印预览
	report.PrintPreview(true);
}

mainForm.btnCustomPreview.oncommand = function(id,event){
	//自定义打印预览
	var printViewForm,wb = mainForm.loadForm("\res\printViewForm.aardio",report);
	printViewForm.doModal();
}


//执行函数
mainForm.rdoPreview.oncommand();//单击预览显示单选按钮
initTreeview();//初始化树形框




mainForm.enableDpiScaling();
mainForm.show();

return win.loopMessage(); 
