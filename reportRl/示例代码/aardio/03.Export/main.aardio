import win.ui;
_IMPORTURL["reportRl"] = "https://github.com/drunkenOstrich/reportRl/releases/latest/download/reportRl.tar.lzma"
import reportRl;
import reportRl.path;
import fsys.dlg;
/*DSG{{*/
mainForm = win.form(text="锐浪报表数据导出 - aardio例程";right=448;bottom=406;border="dialog frame";max=false)
mainForm.add(
btnExit={cls="button";text="退出";left=251;top=366;right=437;bottom=390;z=13};
btnExport={cls="button";text="导出";left=200;top=150;right=386;bottom=174;z=8};
btnExportAndMail={cls="button";text="导出并发送电子邮件";left=200;top=181;right=386;bottom=205;z=9};
btnLoad={cls="button";text="载入并查看...";left=118;top=300;right=304;bottom=324;z=11};
btnPrintPreview={cls="button";text="打印预览";left=11;top=366;right=197;bottom=390;z=12};
btnSave={cls="button";text="保存为...";left=118;top=269;right=304;bottom=293;z=10};
ckbOnlyExportDG={cls="checkbox";text="只导出明细网格";left=201;top=86;right=332;bottom=109;z=6};
ckbShowOptionDlg={cls="checkbox";text="显示选项对话框";left=201;top=56;right=332;bottom=79;z=5};
ckbSuppessEmptyLines={cls="checkbox";text="压缩空白行";left=201;top=116;right=332;bottom=139;z=7};
groupbox={cls="groupbox";text="数据导出";left=12;top=19;right=438;bottom=225;edge=1;z=1};
groupbox2={cls="groupbox";text="文档文件";left=12;top=240;right=438;bottom=352;edge=1;z=2};
listbox={cls="listbox";left=21;top=58;right=145;bottom=211;edge=1;items={"Excel";"PDF";"RTF(Word)";"Text";"CSV";"Html";"Image"};z=4};
static={cls="static";text="导出文件类型：";left=20;top=41;right=113;bottom=60;transparent=1;z=3}
)
/*}}*/

var report = reportRl();
assert(report,"报表组件创建失败！");

var fileName = reportRl.path.templatePath() + "1a.简单表格.grf";
report.LoadFromFile(fileName);
report.DetailGrid.Recordset.ConnectionString = "Provider=Microsoft.Jet.OLEDB.4.0;User ID=Admin;Data Source=" + reportRl.path.dataPath() + "NorthWind.mdb;";

report.ExportBegin = function(pOptionObject){
	/*在报表导出任务执行之前触发本事件。*/
	var showOptionDlg = mainForm.ckbShowOptionDlg.checked;
	var fileName = ..io.fullpath("\export"++mainForm.extName);
	
	//pOptionObject.AbortOpenFile = true; 不使用关联程序打开导出的文件
	pOptionObject.AbortShowOptionDlg = !showOptionDlg;
	pOptionObject.FileName = fileName;
	
	var onlyExportDetailGrid = mainForm.ckbOnlyExportDG.checked;
	var supressEmptyLines = mainForm.ckbSuppessEmptyLines.checked;
	
	var exportType = pOptionObject.ExportType;
	
	select(exportType) {
		case 1/*_gretXLS*/ {
			pOptionObject.AsE2XLSOption.OnlyExportDetailGrid = onlyExportDetailGrid;
			pOptionObject.AsE2XLSOption.SupressEmptyLines = supressEmptyLines;

			pOptionObject.AsE2XLSOption.ExportPageHeaderFooter = false;
			pOptionObject.AsE2XLSOption.SameAsPrint = false;
			pOptionObject.AsE2XLSOption.ExportPageBreak = false;
		}
		case 2/*_gretTXT*/ {
			pOptionObject.AsE2TXTOption.OnlyExportDetailGrid = onlyExportDetailGrid;
			pOptionObject.AsE2TXTOption.SupressEmptyLines = supressEmptyLines;
		}
		case 3/*_gretHTM*/ {
			pOptionObject.AsE2HTMOption.OnlyExportDetailGrid = onlyExportDetailGrid;
			pOptionObject.AsE2HTMOption.SupressEmptyLines = supressEmptyLines;
		}
		case 4/*_gretRTF*/ {
			pOptionObject.AsE2RTFOption.OnlyExportDetailGrid = onlyExportDetailGrid;
			pOptionObject.AsE2RTFOption.SupressEmptyLines = supressEmptyLines;
		}
		case 5/*_gretPDF*/ {
			pOptionObject.AsE2PDFOption.Author = "My Author";
			pOptionObject.AsE2PDFOption.Subject = "My Subject";
		}
		case 6/*_gretCSV*/ {
			pOptionObject.AsE2CSVOption.OnlyExportDetailGrid = onlyExportDetailGrid;
			pOptionObject.AsE2CSVOption.SupressEmptyLines = supressEmptyLines;
		}
		case 7/*_gretIMG*/ {
			pOptionObject.AsE2IMGOption.DPI = 300;
			pOptionObject.AsE2IMGOption.ImageType = 2/*_greitPNG*/;
		}
	}
	
	if(mainForm.isToEmail){
		pOptionObject.MailExportFile = true;
		pOptionObject.MailTo = "name@domain.com";
		pOptionObject.MailSubject = "报表导出并发送Email";
		pOptionObject.MailText = "报表导出并发送Email的相关说明...";
	}
}

var doExport = function(){
	
	var exportType = 1;
	mainForm.extName = ".xls"
	if(mainForm.listbox.selIndex == 1){/*Excel*/
		exportType = 1/*_gretXLS*/;
		mainForm.extName = ".xls"
	}
	if(mainForm.listbox.selIndex == 2){/*PDF*/
		exportType = 5/*_gretPDF*/;
		mainForm.extName = ".pdf"
	}
	if(mainForm.listbox.selIndex == 3){/*RTF(Word)*/
		exportType = 4/*_gretRTF*/;
		mainForm.extName = ".rtf"
	}
	if(mainForm.listbox.selIndex == 4){/*Text*/
		exportType = 2/*_gretTXT*/;
		mainForm.extName = ".txt"
	}
	if(mainForm.listbox.selIndex == 5){/*CSV*/
		exportType = 6/*_gretCSV*/;
		mainForm.extName = ".csv"
	}
	if(mainForm.listbox.selIndex == 6){/*Html*/
		exportType = 3/*_gretHTM*/;
		mainForm.extName = ".htm"
	}
	if(mainForm.listbox.selIndex == 7){/*Image*/
		exportType = 7/*_gretIMG*/;
		mainForm.extName = ".tif"
	}
	
	report.ExportDirect(exportType,..io.fullpath("\export"++mainForm.extName),mainForm.ckbShowOptionDlg.checked,true);
}


mainForm.btnExit.oncommand = function(id,event){
	//退出
	mainForm.close();
}

mainForm.btnExport.oncommand = function(id,event){
	//导出;
	mainForm.isToEmail = false;
	doExport();
}

mainForm.btnExportAndMail.oncommand = function(id,event){
	//导出并发送Email
	mainForm.isToEmail = true;
	doExport();
}

mainForm.btnPrintPreview.oncommand = function(id,event){
	//打印预览
	report.PrintPreview(true);
}

mainForm.btnSave.oncommand = function(id,event){
	//保存为
	var fileName = fsys.dlg.save("Grid++Report Document Files|*.grd|所有文件|*.*|",,,,,"Grid++Report.grd");
	report.GenerateDocumentFile(fileName);
	
}

mainForm.btnLoad.oncommand = function(id,event){
	//载入并查看
	var fileName = fsys.dlg.open("Grid++Report Document Files|*.grd|");
	
	var frmChild,wb = mainForm.loadForm("\res\printViewForm.aardio",fileName);
	frmChild.doModal();

}


mainForm.listbox.selIndex = 1;

mainForm.enableDpiScaling();
mainForm.show();

return win.loopMessage(); 
