
namespace aWord{
    
    writeRowData = function(wd, fields, row){
    	for(i=1; #fields; 1){
			var k = fields[i];
			var v = row[k];
			
			if(..string.startWith(k, "~")){ // 首字符为 '~' 全文查询替换文本
				var text = ..string.right(k, #k - 1);
				wd.replace(text, v);
			}
			else { //替换书签文本内容
				wd.insertBookmarkValue(k, v);
			}
		}
    }
    
    //批量生成文档
	batchCreate = function(template, data, savePath){
		import word;
		var wd = word();
		wd.Visible = true;
		
		for(i=1; #data ;1){
			var doc = wd.Documents.Open(template);
			
			var row = data[i];
			
			//用数据库方式获取的Excel数据，会获取不到在数字列的字符串内容
			//需要通过data.fields[i]取字段名
			self.writeRowData(wd, data.fields, row);
			
			var fileName = !row["文件名"]? tostring(i) : row["文件名"];
			doc.SaveAs(savePath ++ "\" ++ fileName);
			doc.Close(false);
		}
		
		wd.Quit();
	}
	
	//批量修改文档
	batchEdit = function(files, data){
		import word;
		var wd = word();
		wd.Visible = true;
		
		var row = data[1];
		
		for(i=1; #files ;1){
			var doc = wd.Documents.Open(files[i]);
			
			//用数据库方式获取的Excel数据，会获取不到在数字列的字符串内容
			//需要通过data.fields[i]取字段名
			self.writeRowData(wd, data.fields, row);
			
			doc.Save();
			doc.Close(false);
		}
		
		wd.Quit();
	}
	
}
