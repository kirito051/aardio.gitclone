import fsys;
import winex;
import win.ui;
import win.clip; 
import console;
import string.xml;
import string.html;
import thread.command;
/*DSG{{*/
mainForm = win.form(text="爬代理";right=471;bottom=575;border="dialog frame";max=false;min=false)
mainForm.add(
edit={cls="edit";text="https://www.xicidaili.com/nn/";left=8;top=8;right=384;bottom=40;bgcolor=16777215;edge=1;font=LOGFONT(h=-20);multiline=1;readonly=1;z=2};
listview={cls="listview";left=8;top=48;right=464;bottom=568;edge=1;z=3};
抓取={cls="button";text="抓取";left=392;top=8;right=464;bottom=40;z=1}
)
/*}}*/

//https://www.xicidaili.com/nn/
//https://www.kuaidaili.com/free/inha/
 
var listener = thread.command();

//代码显示名称
import win.ui.atom;
var atom,hwnd = mainForm.atom("6A99BEF6-4024-4137-9C44-7FE1BADCF5E3.padaili");
if(!atom){ 
    ///win.showForeground(hwnd);
	win.quitMessage();	
	return;
}
var p1,p2,p3="","","";
if(#_ARGV>0){p1=_ARGV[1];}
if(#_ARGV>1){p2=_ARGV[2];}
if(#_ARGV>2){p3=_ARGV[3];}

if("AUTORUN"!=p1){p1=""}
if("AUTOSAVE"!=p2){p2="";p3="";}

if(io.exist(p3)){ win.msgboxErr(p3++"已存在","错误",mainForm.hwnd );mainForm.close();  }


listener.print=function(tmpfile){
	var i=1;
	///mainForm.listview.addItem( { text={ i; tmpfile }  } );
	var html=  string.load(tmpfile);
	for tables_ip_list in string.gmatch(html,'<@@<table id="ip_list"@>(.+?)<@</table>@>') {
		for tr in string.gmatch(tables_ip_list,'<@<tr@>(.+?)<@</tr>@>') {
			tr="<tr "++tr++"</tr>";		
			var arrtd=string.html(tr).queryEles(tagName="td");
			if(arrtd && #arrtd>3){
				var IP地址=string.xml( tostring(arrtd[2])).innerText();
				var 端口=string.xml( tostring(arrtd[3])).innerText();
				var 服务器地址=string.xml( tostring(arrtd[4])).innerText();
				var 是否匿名=string.xml( tostring(arrtd[5])).innerText();
				var http=string.xml( tostring(arrtd[6])).innerText();
 				mainForm.listview.addItem( { text={  i; tostring(是否匿名) ; tostring(http)++"://"++ tostring(IP地址)++":"++tostring(端口) }  } ); 
				i++;	
			} 
		}
	}
	mainForm.listview.adjust();
	mainForm.listview.redraw();
	publish("抓取结束"); 
}

get_proxy=function(url,tmpfile){    
	thread.invoke(
    	function(sURL,tmpfile, listener ){
    		import inet.http; 		
    		import time;
    		import fsys.file;
    		var can_http_get=true;
			if(io.exist(tmpfile)){
				var aaa=string.load(tmpfile);
				if(#aaa){
					var realhtml = fsys.file(tmpfile, "r");
					var tnow = time.now().addhour(-24); //近24个小时.
					var tttm = realhtml.getTime().write;
					if (tttm > tnow) {
						can_http_get=false;
					}
				}
			}
/*
			if(!io.exist(tmpfile)){	
				can_http_get=true;
			}else{	
				var aaa=string.load(tmpfile);
				if(!#aaa){	
					can_http_get=true;
				}else{
					var realhtml = fsys.file(tmpfile, "r");
					var tnow = time.now().addhour(-24); //近1个小时.
					var tttm = realhtml.getTime().write;
					if (tttm < tnow) { 
						can_http_get=true;
					}
				}
			}
*/			
			if(can_http_get){
				string.save(tmpfile,"" );	
    			var http = inet.http("Mozilla/5.0 (Windows NT 10.0; WOW64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/53.0.2785.104",);
    			http.setTimeouts(10*1000,10*1000,10*1000);
    			http.referer = sURL;
    			http.addHeaders = {["Accept"]="*/*";["Connection"]="Keep-Alive"};
    			var html,err,errCode = http.get(sURL);
    			http.close();        
    			if(html) string.save(tmpfile,html );			
			}
        	listener.post("print",tmpfile);
    	},string.trim(url),tmpfile,listener
    ) 
}
subscribe("抓取进行时",function(...){
	mainForm.抓取.disabled=true;
	mainForm.edit.disabled=true;
} )

subscribe("抓取结束",function(...){
	mainForm.抓取.disabled=false;
	mainForm.edit.disabled=false;	
	if(#p1 && #p2){
		var items=mainForm.listview.items;
		if(#items){ }
		else{ win.msgboxErr("没有抓起到数据,可能是网络不通","错误",mainForm.hwnd);winex.close(mainForm.hwnd); }
	}
} )


mainForm.抓取.oncommand = function(id,event){
	var url=mainForm.edit.text; 
	if(#url){
		mainForm.listview.clear();
		publish("抓取进行时",);
		var tmpfile=fsys.getTempDir()+"\proxy.html";
		get_proxy(url,tmpfile);
	}
	
}

mainForm.onOk = function(){
    mainForm.抓取.oncommand();
}

import win.ui.menu;
mainForm.popmenu = win.ui.popmenu(mainForm);//创建弹出菜单
mainForm.popmenu.add('  复制  ',function(id){ 
	var v=mainForm.listview.selIndex;
	var proxy=mainForm.listview.items[v][3];
	win.clip.write(proxy);
});
mainForm.popmenu.add('  删除  ',function(id){  
	var v=mainForm.listview.selIndex;
	mainForm.listview.delItem(v);
});
mainForm.popmenu.add();
mainForm.popmenu.add('  帮助  ',function(id){  
	var frmChild = mainForm.loadForm($"\dlg\帮助.aardio");
	frmChild.show()
	frmChild.doModal(); 

});

//////////////////////////////////////////////////


mainForm.listview.onnotify = function(id,code,ptr){     
    select(code) { 
    	case 0xFFFFFFFB/*_NM_RCLICK*/  {  //右键菜单    	 
    		if(mainForm.listview.selIndex){ 
    			var x,y = win.getCursorPos();
    			mainForm.popmenu.popup(x-8,y-8,true);//弹出菜单
    		} 
    	}
    }    
}

mainForm.listview.insertColumn("",32,,0x3/*_LVCFMT_LEFT*/) 
mainForm.listview.insertColumn("匿",44,,0x3/*_LVCFMT_LEFT*/) 
mainForm.listview.insertColumn("代理地址",-1,,0x0/*_LVCFMT_LEFT*/)
 
mainForm.listview.fullRow=true;
mainForm.listview.gridLines=true; 

///自适应列宽度 
mainForm.listview.adjust = function(cx,cy){
    mainForm.listview.fillParent(3);
} 

mainForm.setTimeout(
	function(){
		if(#p1){ mainForm.抓取.oncommand(); }
	},100
);

mainForm.setInterval( 
	1000,function(){
		if(!#p1) return;
		if(!#p2) return;
		var items=mainForm.listview.items;
		var proxylines="";
		if(!#items) return;
		for(v=1;#items;1){
			var proxy=items[v][3];
			proxylines+= ( proxy++'\r\n');
		}		
		if(#proxylines){
			///console.varDump(proxylines,p3);
			if(io.exist(p3)){ win.msgboxErr(p3++"已存在","错误",mainForm.hwnd ); }
			else { string.save(p3,proxylines ); }
			p1=""; p2="" ;
			winex.close(mainForm.hwnd)
		}		
	} 
) 

if(!#p2){ mainForm.show();}
return win.loopMessage();