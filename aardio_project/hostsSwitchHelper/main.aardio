//RUNAS//
import iniHosts;
import fsys.hosts;
import chrome.path;
import chrome.app;
import process;
import winex;

var app = chrome.app();  

//external对象定义的函数在Chrome中通过aardio.js可以直接调用
app.external = {
	
	//获取hosts
	loadSystemHostText = function(){ 
		return fsys.hosts.loadText();
	}
	
	//手动刷新dns缓存
	clearDnsCache = function(){
		fsys.hosts.flushDns();
		
		var prc = process(chrome.path())
		var hwnd = winex.waitEnabled(,,,,prc.id)
		win.delay(500)
		key.combine("CTRL","SHIFT","DELETE") 
		win.delay(1000) 
		key.repeat("ENTER",2)
		win.delay(1000)
		key.combine("ALT","F4")  
		return true;
	}
	
	//新建配置
	createSection = function(name){
		return  iniHosts().createSection(name);
	}
	
	//删除配置
	delSection = function(name){ 
		return  iniHosts().delSection(name);
	}
	
	//获取全部配置
	getAllSections = function(){ 
		return  iniHosts().getAllSections();
	}
	
	//更新
	updateSection = function(oldName,newName,status,data){
		return  iniHosts().updateSection(oldName,newName,status,data);
	}
}
	
app.onError = function($hSocket,err){
	errput(err,"chrome/rpc error");
}

var url = "/web-ui/index.aardio" 
if(_STUDIO_INVOKED  ){
	import web.rest.client;
	if( web.rest.client().api("http://localhost:8080/web-ui/",,"hosts切换助手").get() ){
		//如果在前端项目里执行了 npm run dev 下面的调试URL就是可用状态
		url = "http://localhost:8080/web-ui/";	
	} 
}

app.start(url);
win.loopMessage();

	