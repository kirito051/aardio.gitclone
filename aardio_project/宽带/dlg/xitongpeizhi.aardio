import console;
import win;
import win.ui;
import thread;
import thread.table;
import thread.command;
/*DSG{{*/
var winform = win.form(text="系统配置";right=599;bottom=359)
winform.add(
button={cls="button";text="浏览 . .";left=512;top=64;right=576;bottom=88;hide=1;z=6};
button2={cls="button";text="加载数据";left=88;top=104;right=496;bottom=144;z=7};
button3={cls="button";text="浏览 . . ";left=512;top=24;right=576;bottom=48;z=3};
edit={cls="edit";left=88;top=64;right=496;bottom=88;bgcolor=16777215;edge=1;hide=1;readonly=1;z=5};
edit3={cls="edit";left=88;top=24;right=496;bottom=48;bgcolor=16777215;edge=1;readonly=1;z=2};
edit4={cls="edit";left=24;top=160;right=576;bottom=328;bgcolor=16777215;edge=1;multiline=1;readonly=1;vscroll=1;z=9};
groupbox={cls="groupbox";text="地市映射";left=16;top=144;right=584;bottom=336;edge=1;z=8};
static={cls="static";text="地市映射";left=24;top=24;right=80;bottom=48;align="right";center=1;transparent=1;z=1};
static2={cls="static";text="当月修障";left=24;top=64;right=80;bottom=88;align="right";center=1;hide=1;transparent=1;z=4}
)
/*}}*/

var 换行符='\r\n';
var listener = thread.command();
var thrdTable = thread.table("换行符",true/*清空*/ )
var sqlitedb=..io.fullpath("~/sqlite3/kuandai.sqlite")
thread.set("sqlite", sqlitedb);
 
listener.运行状态=function(状态){
	publish("运行状态",状态);
}

listener.加载数据=function(txt){
	publish("加载数据",txt);
}

var 加载地市数据=function(txt){
	thread.set("batch_no", string.random(10));
	thread.invoke( 
		function(txt){
			import console;
			import io;
			import fsys;
			import java;		
			import time.ole;
			import thread;
			import thread.table;
			import thread.command;
			var PID="dishiyingshe";
			var jre8=  ..io.fullpath("~/java/jre8") 
		
			if( ..fsys.isDir(jre8)){			
			}else{
				var jre_zip= ..io.fullpath("~/java/jre8.zip") 
				if( ..io.exist( jre_zip ) ){
					import zlib; 
					import zlib.unzip; 
					zlib.unzip.extract(jre_zip) 
					sleep(1000)
				}
			}
			
			..java.addClassPath(..fsys.getParentDir(thread.get("corejar")))
			..java.addClassPath({"~" ; "~/config/" ;"~/java/" })
			if( (..java.path.findJre(jre8 ) && ..java.path.findJvm(jre8 )) ){
				..java.path.setJreHome(jre8)
			}else{
				import java.jre.v8; //win10:加上这句才会自动下载jre
			}
			
			var import_sqlite3_load_dish,err = java.popenProcess( 
				"-DPID="+PID,
				"-Dbatch_no="+thread.get("batch_no"),
				"-Dfile.encoding=UTF-8",
				"-Dkey="+thread.get("关键KEY"):"",
				"-DDB_PATH="+thread.get("sqlite"):"" ,
				"-Dcheck=false",
				"-Dload_dishi=true", "-Ddishi="+txt ,
			//	"-Dload_Zhuangji=true", "-Dzhuangji="+opt.zhuangji,
			//	"-Dload_Xiuzhang=true", "-Dxiuzhang="+opt.xiuzhang,
				"bin.import_sqlite3"
			);		
			sleep(1000) 
			
			for( all,out,err in import_sqlite3_load_dish.each() ){
				if(err) error("import_sqlite3_load_dish,"++err);
				//console.log( out,err ); 
			}
			import_sqlite3_load_dish.close();
			thread.command.post("运行状态","完成");
			thread.command.post("加载数据",txt);
			
		},txt
	)
}


winform.button3.oncommand = function(id,event){
	import fsys.dlg ;
	var str= fsys.dlg.open("地市映射 *.txt|*.txt;|所有文件 *.*|*.*","地市映射",,winform.hwnd) ;
	if(io.exist(str)) {
		winform.edit3.path=..io.fullpath(str); 
		winform.edit3.text=..fsys.getFileName(winform.edit3.path) ; 
	}
}

winform.button2.oncommand = function(id,event){
	var txt=winform.edit3.path;
	if(!#txt){
		//win.msgboxErr("请选择文件","错误",winform.hwnd);	
		winform.button3.oncommand();
		txt=winform.edit3.path;
	}
	if(#txt){ 
		thread.command.post("运行状态","开始");
		winform.edit4.text="";
		加载地市数据(txt);		
	} 
	
}


subscribe("加载数据",function(txt){
	import sqlite;
	var db = sqlite( thread.get("sqlite") ); 
	if (db) {
		for id,  di_shi, chu_li_ren_ji_gou  in db.each("SELECT  id, di_shi, chu_li_ren_ji_gou FROM [dishiyingshe]") {
    		winform.edit4.appendText( di_shi+" = "+ chu_li_ren_ji_gou +换行符);    		
		}
		db.close();
	} else {
		winform.button2.disabled=true;
		win.msgboxErr("没找到数据库","错误",winform.hwnd);
	}
	

} )

subscribe("运行状态",function(状态){
	if("完成"==状态){
		winform.button2.disabledText=null;
		winform.button3.disabled=false ;
		winform.edit3.path="";
	}else{
		winform.button2.disabledText = {"|";"\";"-";"/"}
		winform.button3.disabled= true ; 
	}	
} )


win.setTimeout(
	function(){
		thread.command.post("加载数据","null");
	},200
)

winform.show();
win.loopMessage();
return winform;