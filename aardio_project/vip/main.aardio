import debug ;
import win.ui;
import web.form;
import web.kit;
import web.kit.form;
import fsys.ini;
import console;
import Utilss;
import thread;
/*DSG{{*/
mainForm = win.form(text="youku-vip";right=959;bottom=591)
mainForm.add(
button={cls="button";text="打开";left=623;top=16;right=712;bottom=48;aw=1;font=LOGFONT(h=-20);z=2};
button2={cls="button";text="VIP";left=871;top=16;right=960;bottom=48;aw=1;disabled=1;font=LOGFONT(h=-20);z=3};
button3={cls="button";text="<";left=16;top=16;right=48;bottom=48;font=LOGFONT(h=-20);z=6};
button4={cls="button";text=">";left=48;top=16;right=80;bottom=48;font=LOGFONT(h=-20);z=7};
combobox={cls="combobox";left=720;top=16;right=864;bottom=42;aw=1;edge=1;font=LOGFONT(h=-20);items={};mode="dropdownlist";z=5};
custom={cls="custom";text="自定义控件";left=8;top=80;right=960;bottom=592;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;z=4};
edit={cls="edit";left=79;top=16;right=615;bottom=48;aw=1;edge=1;font=LOGFONT(h=-20);z=1}
)
/*}}*/

ini = fsys.ini(".sys.ini");
var interface = ini.read("默认值", "interface");
if (#interface) {
}else{
    interface = "https://www.eggvod.cn/jx.php?lrspm=29211626&zhm_jx=https://v.youku.com/v_show/id_XNDUyMDA1MjEwMA==.html?";
    ini.write("默认值", "interface", interface);
}
thread.set("interface",string.trim(interface));


/**获得接口的实际URL**/
get_real_inter = function(name) {
    var vip_url = null;
    var tabs_vip = thread.get("tabs_vip");
    if (!tabs_vip) return;
    for (index, vvv in tabs_vip) {
        var text = vvv.text;
        var value = vvv.value;
        var qqq = index++"-"++text;
        if (qqq == name) {
            vip_url = value;
        }
    }
    return vip_url;
}



/**注册一个通用的消息处理中心**/
var child_thrdHandle, child_thrdId = thread.create(function(arg) {
        //线程函数内部要添加自已的import语句
        import win;
        import console; 
        import Utilss; 
        import Cache; 
        
		clean_cache_33001=function(){
    		var interface = thread.get("interface");
    		if (!#interface)  return ;
    		var vip_url_md5=Utilss.getMd5(interface);
        	Cache.clean(vip_url_md5);    			
		}

        //在子线程启动消息循环        
        win.loopMessage(
            function(msg) {           	
                if (33001 == msg.message /**清理缓存**/ ) { clean_cache_33001(); }
        })
})
    
/**起一个线程处理VIP线路加载事宜**/
thread.invoke(function(mainForm, interface) {
    import console;
    import Utilss;
    import fsys;
    import fsys.table ;
    var util = Utilss();
    var tabs_vip = fsys.table(".tabs_vip.table");
    if(!#tabs_vip){
    	var optales = util.get_inter_list(interface);
    	if (!#optales) {
       		mainForm.msgbox("没找到VIP接口，请重试！")
    		return ;    	
    	}
    	for (index, value in optales) {
        	if ( index > 20 )  continue; //break不行
        	var text = optales[index][1]['text'];
        	var value = optales[index]['value'];
        	var tt = {};
        	tt.index = index;
        	tt.text = text;
        	tt.value = value;
        	table.push(tabs_vip, tt);         		
    	}
    	tabs_vip.save(); 
    }

    thread.set("tabs_vip", tabs_vip);  
    for (index, vvv in tabs_vip) { 
        if(type(vvv)!='table') continue; 
        var value = vvv.value;
        var text = vvv.text;
		mainForm.combobox.add(index ++ "-" ++ text);        
    }
    
    mainForm.combobox.selIndex = 1;
    mainForm.button2.disabled = false; 
    
}, mainForm, interface)


///web.form.emulation(9999);

var wb=null;
var brower_type= ini.read("默认值", "brower");
if(#brower_type && ..string.cmpMatch(brower_type ,"ie")){
	wb = web.form(mainForm.custom, , , , true);
} else{
	wb = web.kit.form(mainForm.custom);
	wb.userAgent = "User-Agent:Mozilla/5.0 (Windows NT 10.0; WOW64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/53.0.2785.104 Safari/537.36 Core/1.53.4033.400 QQBrowser/9.6.12624.400";
	wb.cookieEnabled = true;
}

wb.noScriptErr = true;

wb.NavigateComplete2 = function(pDisp, url) {
    /// mainForm.edit.text=wb.location;
}
mainForm.edit.text = ini.read("默认值", "网址");
play = function(inter, url) {
   /// debug.log(inter, url);
   	wb.noScriptErr = true;
    wb.go(inter++url);
}
///////////////////////////////////////////////////
mainForm.button.oncommand = function(id, event) { /**打开-BTN**/
    var text = mainForm.edit.text;
    if (!#text) {
        mainForm.edit.setFocus(); 
    }else{
    	wb.go(text);
    	ini.write("默认值", "网址", string.trim(text));
    }
}
///////////////////////////////////////////////////
mainForm.button2.oncommand = function(id, event) { /**VIP-BTN**/
    var url = wb.location;
    var inter_name = mainForm.combobox.selText;
    if (#url && #inter_name) {
        var vip_url = get_real_inter(inter_name);
        if (#vip_url){
         	play(vip_url, url);
       }
    }
}
///////////////////////////////////////////////////
mainForm.button2.wndproc = function(hwnd, message, wParam, lParam) { /**VIP-BTN**/
    select(message) { //判断消息类型 
        case 0x0203 /*_WM_LBUTTONDBLCLK; 双击左键 */ {
        	::PostThreadMessage(child_thrdId, 33001, 0, 0); 
        }
    }
}
///////////////////////////////////////////////////
mainForm.button3.oncommand = function(id, event) {
    wb.doScript("window.history.go(1);");
    //wb.goBack()
}
///////////////////////////////////////////////////
mainForm.button4.oncommand = function(id, event) {
    //wb.goForward();
    wb.doScript("window.history.go(1);");
}
/**关闭时通知消息处理器退出**/
mainForm.onClose = function(hwnd, message, wParam, lParam) {
    //给线程发送退出消息
    ::PostThreadMessage(child_thrdId, 0x12 /*_WM_QUIT*/ , 0, 0)
    thread.waitClose(child_thrdHandle);
}
mainForm.show();
return win.loopMessage();