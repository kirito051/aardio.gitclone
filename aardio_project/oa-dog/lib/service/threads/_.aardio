//服务处理程序
namespace service.threads;

import service;
import config;
import console;
import thread;
import thread.event;  
import yh.log;
import yh.util;
import yh.oa;

util,log,oa=yh.util,yh.log,yh.oa
 
换行符=util.换行符; 

LOG_INFO=function(...){log.LOG_INFO(...); }
LOG_DEBUG=function(...){log.LOG_DEBUG(...); }
LOG_ERROR=function(...){log.LOG_ERROR(...); }
	
//服务主线程
main = function(serviceName,ARGV){
	
	LOG_INFO("服务主线程启动"); 

	h获取启动参数=function(name,默认值){ return util.获取启动参数(ARGV,name)||默认值;  }

	用户名=h获取启动参数("-user","user")  ;
	密码=h获取启动参数("-pswd","pswd")  ;	
	检查时间=h获取启动参数("-检查时间","1")  ;
	超时时间=1*60*1000;//一分钟	

	LOG_INFO("[参数]用户名",用户名,""); 
	LOG_INFO("[参数]密码","xxx",""); 
	LOG_INFO("[参数]检查时间",检查时间,"分钟");
	LOG_INFO("[参数]超时时间",超时时间,"");  
	
	try{
		检查时间=tonumber(检查时间);
		检查时间=1000*6*检查时间;
		if(检查时间<1) error("检查时间不能小于1"); 
		if(检查时间<=2){ 超时时间=0.5*超时时间; }
	} catch(e){ 
		LOG_ERROR("检查时间","不是正确的数字",tostring(e));
		return -1;	
	} 
	
	var 参数信息={
		用户名=用户名;
		密码=密码;
		超时时间=超时时间;
	}
	var evt = thread.event("NT.SERVICE.CONTROL." + serviceName,false);
	while(  ! evt.wait(检查时间)  ) {  //每[检查时间]分执行一次
		LOG_DEBUG("检查邮件信息");
		try{
			var 邮件信息=oa.获取邮件信息(参数信息); 
			LOG_DEBUG("获取邮件信息数量",#邮件信息 );			
			if(#邮件信息>3){
				var str=..string.format("有 %d 封新邮件,请查看" ,#邮件信息 ) ;
				service.msgbox(str,"邮件信息",10*1000);				
			}else{
				var str="";
				for(v=1;#邮件信息;1){
					var 邮件=邮件信息[v];
					str=str+ 邮件.标题+换行符;
				}
				service.msgbox(str,"邮件信息",10*1000);
			}
		}catch(e){
			LOG_ERROR("获取邮件信息错误",tostring(e));		 
		} 
	}
	
}


//注意这也是一个独立的线程，用户控制服务停止或启动时都会触发此函数
control = function(control,eventType,pEventData){ 
    import service;
    import thread.event;
    
    var evtTask = thread.event("NT.SERVICE.CONTROL." + owner.serviceName,false);     
    if( control == 0x1/*_SERVICE_CONTROL_STOP*/ ){  
        //控制线程中更新srvStatus，第一个参数只要指定需要更新的字段就行了
        service.updateStatus({currentState = 0x3/*_SERVICE_STOP_PENDING*/},owner.serviceName);
        evtTask.set();//使事件对象切换为有信号状态,使wait函数退出  
    }
     
    return 0x0/*_NOERROR*/
}
