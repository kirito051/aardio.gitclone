import console;
import win.ui;
/*DSG{{*/
var winform = win.form(text="抽奖";right=703;bottom=620;border="none";exmode="toolwindow";max=false;min=false;mode="popup")
winform.add(
static={cls="static";text="下一个就是你哦";left=0;top=136;right=704;bottom=392;ah=1;align="center";aw=1;center=1;color=128;db=0.37;dl=1;dr=1;font=LOGFONT(h=-80;name='微软雅黑');notify=1;transparent=1;z=4};
static2={cls="static";text="大奖";left=8;top=16;right=704;bottom=112;align="center";center=1;dl=0.01;dt=0.03;font=LOGFONT(h=-53;name='微软雅黑');transparent=1;z=3};
开始={cls="button";text="开始";left=216;top=472;right=488;bottom=552;db=1;dl=1;dr=1;font=LOGFONT(h=-53;name='微软雅黑');z=1};
恭喜={cls="button";text="恭喜";left=216;top=472;right=488;bottom=552;db=1;dl=1;dr=1;font=LOGFONT(h=-53;name='微软雅黑');z=2}
)
/*}}*/

var args={...} ;
var txttabl={} ;
var 换行符='\r\n';
var PEOPLE="PEOPLE";
var 开始抽奖=false;
var 抽奖次数=0;
var 自动开始= false;

if(#args>1) { 自动开始=(!!( args[2]=="自动开始" )) }

winform.恭喜.oncommand = function(id,event){
	table.clear(txttabl);
	开始抽奖=false;
	抽奖次数=0;
	winform.恭喜.show(false);
	winform.开始.show(false);
	publish("抽奖效果结束","抽奖效果结束")	;
	winform.close();
}

winform.开始.oncommand = function(id,event){
	winform.开始.show(false);
	开始抽奖=true; 	
}

winform.随机人员=function(arrPEOPLE){
    var l=#arrPEOPLE;
    for(i=1;10;1){
		var v=(math.random(1,999))%l;
		var v1=v+1;
		var a=arrPEOPLE[v1];
		if(#a) return a;
    } 
    return "";
}

var times=(math.random(1,1000))%60;
if(times<30) times+=50 ;
elseif(times<50) times+=30 ; //一个大于2000的毫秒数 

winform.onInitDialog = function(hwnd,message,wParam,lParam){	
    抽奖结果_奖项=thread.get("抽奖结果_奖项") ;
    抽奖结果_人员=thread.get("抽奖结果_人员") ;
    PEOPLE=thread.get("PEOPLE") ; 
    var arrPEOPLE=string.split(PEOPLE, 换行符); 
    for(i=1;times;1){
        var 人家=winform.随机人员(arrPEOPLE); 
		if(#人家){ table.insert(txttabl,人家); }
	}
	table.insert(txttabl,抽奖结果_人员[2] ); 
	///	
	var text =  (抽奖结果_奖项[1] ++" "++ 抽奖结果_奖项[3]) ;
	winform.static2.color = math.random(1,16777215);
	winform.static2.text= text;  
	////
	if(自动开始) { win.setTimeout( function(){ winform.开始.oncommand();  } ,1000 )}
	else winform.开始.show();

}

winform.onOk = function(){
 	///if(开始抽奖){ winform.开始.oncommand(); } 
}


/*在窗口内创建热键*/
import win.ui.accelerator;
var accelerator = win.ui.accelerator({
    {  
        vkey = ' '#; ///空格键 
        oncommand = function() { 
        	if(!自动开始 && !抽奖次数 && !开始抽奖 ) { winform.开始.oncommand(); }	
        	if( 抽奖次数 && !开始抽奖 /*抽奖结束*/ ) { winform.恭喜.oncommand(); } 
        } 
    };   
},winform );

winform.setInterval(
	100,function(){   
		winform.static.color =  math.random(1,16777215) ; 	
		winform.static.redraw() ;
		if(开始抽奖){		 	
			if(!#txttabl) { 开始抽奖=false; }
			var t=table.pop(txttabl,1);
			if(#t){ 
				winform.static.text= t ; 
				抽奖次数++;
			}			
		}else{
			if( 抽奖次数>2 ){  
				winform.恭喜.show();				
			}
		}
	}
); 	

winform.恭喜.show(false);
winform.开始.show(false);

winform.show();
win.loopMessage();
return winform;