import process;
import win.ui.menu;
import win.util.tray;
import thread;
import thread.manage;
import win.ui;
import win.ui.atom;
/*DSG{{*/
mainForm = win.form(text="postsuperman";right=624;bottom=480)
mainForm.add(
["button信息"]={cls="button";text="信息";left=256;top=40;right=288;bottom=56;dl=1;dt=1;z=7};
["button发送"]={cls="button";text="发送";left=488;top=16;right=544;bottom=40;default=1;dr=1;dt=1;z=3};
["checkbox格式化"]={cls="checkbox";text="格式化";left=552;top=16;right=616;bottom=32;dr=1;dt=1;z=4};
["combobox类型"]={cls="combobox";left=32;top=16;right=104;bottom=40;dl=1;dt=1;edge=1;items={};mode="dropdown";z=1};
["edit地址"]={cls="edit";left=112;top=16;right=480;bottom=40;dl=1;dr=1;dt=1;edge=1;z=2};
input={cls="richedit";left=0;top=56;right=256;bottom=480;db=1;dl=1;dr=1;dt=1;edge=1;multiline=1;vscroll=1;z=5};
output={cls="richedit";left=256;top=56;right=624;bottom=480;db=1;dl=1;dr=1;dt=1;edge=1;multiline=1;vscroll=1;z=6};
["static信息"]={cls="static";left=296;top=40;right=624;bottom=56;color=255;dl=1;dt=1;transparent=1;z=8}
)
/*}}*/

var atom,hwnd = mainForm.atom("6A99BEE6-4324-4537-9C13-7FE5BADCF5E3.postsuperman");
if(!atom){ 
    win.showForeground(hwnd);
	win.quitMessage();	
	return;
}

var manage=thread.manage(1);

var thrdFunc=function(mainForm,param){
    import time;
	import web.rest.jsonLiteClient;
	mainForm.button发送.disabledText="...";
	var restClient = web.rest.jsonLiteClient("user-agent:Mozilla/5.0 (Linux; U; Android 8.0.0; zh-cn; MI 5 Build/OPR1.170623.032) AppleWebKit/537.36 (KHTML, like Gecko) Version/4.0 Chrome/66.0.3359.126 MQQBrowser/9.6 Mobile Safari/537.36");
	var selText=mainForm.combobox类型.selText;
	var begintime=time.tick();
	if("GET"==selText){ restClient.get(param.地址); }
	else { restClient.post(param.地址,param.input); }
	var endtime=time.tick();
	if(restClient.ok()){
		var lastResponseString=restClient.lastResponseString();
		if(mainForm.checkbox格式化.checked){
			lastResponseString=..web.json.parse(lastResponseString);
			lastResponseString=..web.json.stringify(lastResponseString,true,false);		
		}
		mainForm.output.text=lastResponseString;		
	}
	else{
		var msg,code=restClient.lastStatusMessage();
		var str=..string.format("%s %s %s","错误",code||"-999999", msg||"system error" );
		mainForm.output.text=str;
	}
	var tmpname=io.tmpname()++".txt";
	..string.save(tmpname,table.tostring(restClient));
	thread.set("tmpname",tmpname ); 
	var str=..string.format("耗时:%0.3f秒",((1+endtime-begintime)/1000) );
	mainForm.static信息.text=str;
	mainForm.button发送.disabledText=null;
	mainForm.button信息.show();
	if(restClient) restClient.close();
	return str;
}

mainForm.button发送.oncommand = function(id,event){
	var 地址=mainForm.edit地址.text||"";	
	var input=mainForm.input.text||"";
	if(#地址){
		var param={地址=地址;input=input;};
		mainForm.output.text="";
		mainForm.static信息.text="";
		mainForm.button信息.show(false);
		manage.create(thrdFunc,mainForm,param).onEnd = function(...){ } 
	}
}

mainForm.button信息.oncommand = function(id,event){
	var tmpname=thread.get("tmpname");
	if(io.exist(tmpname)){
		process.execute(tmpname);
	}
}

mainForm.onMinimize = function(lParam){ 
	//mainForm.show(false); 
	//return true; 
}

mainForm.onClose = function(hwnd,message,wParam,lParam){
	mainForm.show(false); 
	if(mainForm.tray){ mainForm.tray.pop(mainForm.text);}
	return true; 
}

mainForm.onDestroy = function(){
    if(manage){ manage.quitMessage(); }
	if(mainForm.tray){ mainForm.tray.delete() }
}

mainForm.wndproc = {
	[0xACCF/*_WM_TRAYMESSAGE*/ ] = function(hwnd,message,wParam,lParam){
		if( lParam = 0x205/*_WM_RBUTTONUP*/ ){
			win.setForeground(mainForm.hwnd)
			mainForm.popmenu = win.ui.popmenu(mainForm);//创建弹出菜单
			mainForm.popmenu.add(mainForm.text ,function(id){
				mainForm.show();
			}); 
			mainForm.popmenu.add();
			
			mainForm.popmenu.add('打开本软件源代码',function(id){
				process.openUrl("https://gitee.com/wmhx/aardio_project/tree/master/postsuperman/")
			});
			mainForm.popmenu.add('打开官网论坛: bbs.aardio.com',function(id){
				process.openUrl("http://bbs.aardio.com")
			});
			mainForm.popmenu.add();
			
			mainForm.popmenu.add('退出',function(id){
				win.quitMessage();
			});
			
			var pt = ::POINT();
			::User32.GetCursorPos(pt);
			mainForm.popmenu.popup(pt.x,pt.y,true)
		}
		elseif( lParam = 0x202/*_WM_LBUTTONUP*/ ) {
			mainForm.show();
			win.setForeground(mainForm.hwnd); 
		} 	
	}
}

mainForm.combobox类型.add("GET");
mainForm.combobox类型.add("POST");
mainForm.combobox类型.selIndex=1;
mainForm.edit地址.text=/**https://www.iesdouyin.com/web/api/v2/hotsearch/billboard/aweme/**/;
mainForm.button信息.show(false);
mainForm.checkbox格式化.checked=true;
//mainForm.checkbox格式化.disabled=true;

mainForm.tray = win.util.tray(mainForm) ;
mainForm.tray.tip=mainForm.text;

mainForm.show();
return win.loopMessage();
