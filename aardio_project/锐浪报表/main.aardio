import time;
import fsys;
import win;
import win.ui;
import win.ui.atom;
import winex;
import winex.mouse;
import mouse;
import com.activeX;
/*DSG{{*/
mainForm = win.form(text="条码打印";right=471;bottom=400;border="thin";max=false;min=false)
mainForm.add(
edit={cls="edit";left=8;top=48;right=216;bottom=392;edge=1;multiline=1;vscroll=1;z=2};
edit2={cls="edit";text="50";left=16;top=8;right=64;bottom=40;align="center";edge=1;font=LOGFONT(h=-20;name='微软雅黑');num=1;z=4};
edit3={cls="edit";text="3";left=72;top=8;right=104;bottom=40;align="center";edge=1;font=LOGFONT(h=-20;name='微软雅黑');num=1;z=5};
edit4={cls="edit";left=232;top=368;right=464;bottom=392;align="center";edge=1;readonly=1;z=8};
static={cls="static";left=232;top=64;right=464;bottom=96;transparent=1;z=6};
static2={cls="static";left=232;top=112;right=464;bottom=144;transparent=1;z=7};
打印预览={cls="button";text="打印预览";left=232;top=8;right=464;bottom=40;z=3};
生成数据={cls="button";text="生成数据";left=112;top=8;right=216;bottom=40;z=1}
)
/*}}*/

var atom,hwnd/*冲突窗口的句柄,该函数会自动激活此窗口*/ 
= mainForm.atom("99AaD633-D6F3-4F7C-A576-98D6F2C3CACC.ruilang-print");
if(!atom){
	win.quitMessage();        
	return;
}

var 换行符='\r\n';
var path=fsys.getCurDir();
var grf=path++"\a.grf";  //模板
var grdes6=path++"\gregn6.dll" ;  //运行模块
var report,xml="","";

gen_xml=function(){	
	var xxxml="<u>"++换行符;
	var text= mainForm.edit.text;
	if(!#text)return;	
	var arrtext= string.split(text,换行符);
	if(!#arrtext)return;
	for v, t in arrtext{
		t=string.trim(t);
		if(#t){
			var n=string.replace(t,"-","");
    		var str="<o><i>"++v++"</i><f>"++t++"</f><n>"++n++"</n></o>";
    		xxxml+=str++换行符;
		}
	}
	xxxml+="</u>";   
	var file=io.tmpname("锐浪");
	var b=string.save(file, xxxml) ;
	if(b) xml=file ;	
}

open_bookMark=function(title){
	try{
		var printhwnd = winex.find( ,title,, );
		printhwnd = winex.findEx(printhwnd,,"AtlAxWinLi\x","",201);
		printhwnd = winex.findEx(printhwnd,,"ATL\:\x","",3792604);
		printhwnd = winex.findEx(printhwnd,,"ToolbarWindow\x","",0);
		winex.mouse.click(printhwnd,13,8);
	}catch(e){
	}
}

mainForm.打印预览.oncommand = function(id,event){
    gen_xml();
	if(#xml && io.exist(xml) ) {	
		mainForm.static2.text=grf;	
		mainForm.static.text=xml;
		report.Title="条码打印 " ++ mainForm.edit4.text; 	
		report.ShowProgressUI(true);
		report.LoadFromFile(grf);
		report.LoadDataFromURL(xml); 
		win.setTimeout( function(){ open_bookMark( string.left(report.Title,20)); },2000 ) ;
		report.PrintPreview(true); 
		xml=""; 
	}
	else { win.msgboxErr("生成XML错误","错误",mainForm.hwnd); } 
}

mainForm.生成数据.oncommand = function(id,event){	
    mainForm.edit.text=""; 
	var tm = time.now();	
	var str  = tostring(tm,"%Y%m%d","chs")
    var vt=tonumber(mainForm.edit2.text);
    var vs=tonumber(mainForm.edit3.text);
    if(vt<1 || vs<1) { win.msgboxErr("数字错误","",mainForm.hwnd);return; } 
	for(i=1;vt;1){		 
		for(k=1;vs;1){		 
			mainForm.edit.appendText(str++"-"++i++换行符); 
		}
		mainForm.edit.appendText(换行符);
	}
}  

mainForm.onOk = function(){
	mainForm.生成数据.oncommand ();
}

///////////////////////////


if(!io.exist(grf)){
	win.msgboxErr(grf++"不存在","",mainForm.hwnd); 
	mainForm.close();
};
	
if(!io.exist(grdes6)){
	win.msgboxErr(grdes6++"不存在","",mainForm.hwnd); 
	mainForm.close();
};	

try{
	com.activeX.regsvr32(grdes6);
	report = com.CreateObject("gregn.GridppReport"); 	
}catch(e){
	win.msgboxErr(grdes6++",锐浪控件注册错误,","",mainForm.hwnd);  
	mainForm.close();	
}

///////////////////////////

mainForm.setInterval(
	1000,function(){
		mainForm.edit4.text= tostring(time());
	}
);

///////////////////////////

var timers=1;
mainForm.setInterval(
	100,function(){
		var t=string.repeat(8, "#");
		var tl=timers%(#t);
		mainForm.text="条码打印 "++string.left(t,tl) ;
		timers+=1;
	}
);

mainForm.show();
return win.loopMessage();