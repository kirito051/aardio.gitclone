namespace cv2
var dll = ..raw.loadDll("~/lib/cv2/.res/OpenCvSharpExtern.dll","opencv2","cdecl")

var f2i = function(p){
    return ::POINT(..math.round(p.x),..math.round(p.y)); 
}

/*imgcodecs{{*/
imread = function(path,mode=1/*_CV2_IMREAD_COLOR*/){
    var path = ..io.fullpath(path)
	if(..io.exist(path)){
		var err,ret = dll.imgcodecs_imread(..string.fromto(path),mode,{ptr p})
		return ..cv2.Mat(ret.p)
	}
	return null;
}

imwrite = function(filename,img,params){
	var ret = {int value}
	var err = dll.imgcodecs_imwrite(filename,img,params ? {int p[]=params},#params,ret)
	return ret.value; 
}

imdecode = function(buffer,flag){
    var buf = ..raw.buffer(buffer)
	var err,ret = dll.imgcodecs_imdecode_vector(buf,#buf,flag,{pointer value})
	return ..cv2.Mat(ret.value); 
}

imencode = function(ext,img,parmas){
	var buf = ..cv2.vector.byte();
	dll.imgcodecs_imencode_vector(ext,img.cvPtr,buf,params ? {int p[]=params},#params,{pointer value});
	var re = buf.toArray();
	buf.release()
	return re
}

/*}}*/

/*highgui{{*/
imshow = function(winName="",matHandle){
    var winName = ..string.fromto( winName )
	dll.highgui_imshow(winName,matHandle)
}

destroyAllWindows = function(){
	dll.highgui_destroyAllWindows()
}

waitKey = function(code=0){
    var err,stu = dll.highgui_waitKey(code,{int val})
	return stu.val
}

namedWindow = function(winName,flag=1/*_CV2_WINDOW_AUTOSIZE*/){
	var err = dll.highgui_namedWindow(winName,flag)
	return err; 
}

cvGetWindowHandle = function(name){
	var err,ret = dll.highgui_cvGetWindowHandle(name,{int value})
	return ret.value; 
}

/*}}*/

/*core{{*/
getTickCount = function(){
    var err,ret = dll.core_getTickCount({LONG value});
	return ret.value
}

core_getTickFrequency = dll.api("core_getTickFrequency","int(double& v)" ,"cdecl")
getTickFrequency = function(){
	var ret,v = core_getTickFrequency(0)
	return v; 
}

copyMakeBorder = function(src,top,bottom,left,right,borderType=0/*_CV2_BorderTypes_BORDER_CONSTANT*/,color){
    var dst = ..cv2.Mat();
    if(borderType==0/*_CV2_BORDER_CONSTANT*/ or !color){
    	color = ::Scalar(0,0,0)
    }
	var err = dll.core_copyMakeBorder(src.cvPtr,dst.cvPtr,top,bottom,left,right,borderType,color)
	return dst; 
}

bitwise_and = function(src1,src2,mask){
    var dst = ..cv2.Mat();  
    mask = mask ? (mask.cvPtr)
	var err = dll.core_bitwise_and(src1.cvPtr,src2.cvPtr,dst.cvPtr,mask);
	return dst; 
}

bitwise_or = function(src1,src2,mask){
    var dst = ..cv2.Mat();
    mask = mask ? (mask.cvPtr)
	var err = dll.core_bitwise_or(src1.cvPtr,src2.cvPtr,dst.cvPtr,null);
	return dst; 
}

bitwise_xor = function(src1,src2,mask){
    var dst = ..cv2.Mat();
    mask = mask ? (mask.cvPtr)
	var err = dll.core_bitwise_xor(src1.cvPtr,src2.cvPtr,dst.cvPtr,null)
	return dst; 
}

bitwise_not = function(src,mask){
    mask = mask ? (mask.cvPtr)
	var err = dll.core_bitwise_not(src.cvPtr,src.cvPtr,mask)
	return src; 
}

core_normalize= dll.api("core_normalize","INT(pointer src,pointer dst,double alpha,double beta,int normType,int dtype,pointer mask)")
normalize = function(src,alpha,beta,normType=0x20/*_CV2_NormTypes_NORM_MINMAX*/,dtype=-1,mask){
	//mask = mask : ..cv2.Mat().cvPtr
	var err = core_normalize(src.cvPtr,src.cvPtr,alpha,beta,normType,dtype,topointer(0))
	return src; 
}

split = function(src){
    var vecMat = ..cv2.vector.mat()
    var err,ret = dll.core_split(src,vecMat.handle);
    var result = ..table.map(vecMat.toArray(),function(v,k,result){
			return v; 
	})
	vecMat.release()
	return ..table.unpack(result)
}

merge = function(srcs){
    //var vecMat = ..cv2.vectorOfMat(srcs)
    var dst = ..cv2.Mat();
    if(#srcs){
 		dll.core_merge({ ptr p[] = srcs },#srcs,dst);   	
    }
	return dst; 
}

flip = function(src,flipCode){
    var dst = ..cv2.Mat();
	var err = dll.core_flip(src.cvPtr,dst.cvPtr,flipCode)
	return dst; 
}

subtract = function(src1,src2,mask,dtype){
    var dst = ..cv2.Mat();
    mask = mask ? mask.cvPtr
	var err = dll.core_subtract_InputArray2(src1.cvPtr,src2.cvPtr,dst.cvPtr,mask,dtype)
	return dst;
}

add = function(src1,src2,mask,dtype){
	var dst = ..cv2.Mat();
	mask = mask ? mask.cvPtr
	var  err = dll.core_add(src1.cvPtr,src2.cvPtr,dst.cvPtr,mask,dtype)
	return dst; 
}

addWeighted = function(src1,alpha,src2,beta,gamma=0,dtype=-1){
    var dst = ..cv2.Mat();
    var err = dll.core_addWeighted(src1.cvPtr,..raw.double(alpha),src2.cvPtr,..raw.double(beta),..raw.double(gamma),dst.cvPtr,dtype);
    return dst; 
}

minMaxLoc = function(src){
    minLoc0 = ::POINT();
    maxLoc0 = ::POINT();
	var err,minVal,maxVal = dll.core_minMaxLoc2(src.cvPtr,..raw.double(0,true),..raw.double(0,true),minLoc0,maxLoc0,null)
	return minVal,maxVal,minLoc0,maxLoc0; 
}

rotate = function(src,rotateCode){
	var dst = ..cv2.Mat();
	var err = dll.core_rotate(src.cvPtr,dst.cvPtr,rotateCode);
	return dst; 
}

inRange = function(src,lower,upper){
   	var tLower = type.rget(lower)
   	var tUpper = type.rget(upper)
   	var dst = ..cv2.Mat()
   	if(tLower=="mat" and tUpper=="mat"){
   		err = dll.core_inRange_InputArray(src.cvPtr,lower.cvPtr,upper.cvPtr,dst.cvPtr)
   	}
   	elseif(tLower=="Scalar" and tUpper=="Scalar"){
   		err = dll.core_inRange_Scalar(src.cvPtr,lower,upper,dst.cvPtr)
   	}
	return dst; 
}

compare = function(src1,src2,cmpop){
    var dst = ..cv2.Mat()
	dll.core_compare(src1.cvPtr,src2.cvPtr,dst.cvPtr,cmpop)
	return dst; 
}

hconcat = function(...){
	var mats = {...}
	return reduce(mats,function(prev,next,index,arr){
		var dst = ..cv2.Mat()
		dll.core_hconcat2(prev.cvPtr,next.cvPtr,dst.cvPtr)
		return dst
	})
}

vconcat = function(...){
	var mats = {...}
	return reduce(mats,function(prev,next,index,arr){
		var dst = ..cv2.Mat()
		dll.core_vconcat2(prev.cvPtr,next.cvPtr,dst.cvPtr)
		return dst
	})
};

self["reduce"] = function(src,dim=-1,rtype,dtype=-1){
	var dst = ..cv2.Mat()
	dll.core_reduce(src.cvPtr,dst.cvPtr,dim,rtype,dtype)
	return dst;
}

setNumThreads = function(nthreads=0){
	var err = dll.core_setNumThreads(nthreads)
}

getNumThreads = function(){
	var err,ret = dll.core_getNumThreads({int nthreads})
	return ret.nthreads; 
}

getVersionString = function(){
	var buffer = ..raw.buffer(0xff)
	var err = dll.core_getVersionString(buffer,0xff)
	return ..raw.str(buffer); 
}
/*}}*/

/*imgproc{{*/
resize = function(src,dsize,fx=1.0,fy=1.0,interpolation=1/*_CV2_INTER_LINEAR*/){
	var dst = ..cv2.Mat();
	dsize = dsize : ::SIZE()
	var err = dll.imgproc_resize(src.cvPtr,dst.cvPtr,dsize,..raw.double(fx),..raw.double(fy),interpolation)
	return dst; 
}

cvtColor = function(src,code,dstCn=0){
    var dst = ..cv2.Mat();
	var err = dll.imgproc_cvtColor(src.cvPtr,dst.cvPtr,code,dstCn)
	assert(!err,"转换失败!")
	return dst;
}

medianBlur = function(src,ksize){
    var dst = ..cv2.Mat()
	var err = dll.imgproc_medianBlur(src.cvPtr,dst.cvPtr,ksize)
	return dst; 
}

GaussianBlur = function(srcMat,ksize,sigmaX=0,sigmaY=0,borderType=4/*_CV2_BORDER_DEFAULT*/){
    var dstMat = ..cv2.Mat()
    err = dll.imgproc_GaussianBlur(srcMat.cvPtr,dstMat.cvPtr,ksize,..raw.double(sigmaX),..raw.double(sigmaY),borderType)
    //err = imgproc_GaussianBlur(srcMat.cvPtr,dstMat.cvPtr,ksize,sigmaX,sigmaY,borderType)
    return dstMat; 
}

blur = function(srcMat,ksize,anchor,borderType=4/*_CV2_BORDER_DEFAULT*/){
	var dstMat = ..cv2.Mat()
	anchor = anchor : ::POINT(-1,-1)
	var err = dll.imgproc_blur(srcMat.cvPtr,dstMat.cvPtr,ksize,anchor,borderType)
	return dstMat; 
}

boxFilter = function(srcMat,ddepth=-1,ksize,anchor,normalize=true,borderType=4/*_CV2_BORDER_DEFAULT*/){
	anchor = anchor : ::POINT(-1,-1)
	var dst = ..cv2.Mat()
	var err = dll.imgproc_boxFilter(srcMat.cvPtr,dst.cvPtr,ddepth,ksize,anchor,normalize,borderType)
	return dst; 
}

bilateralFilter = function(src,d,sigmaColor,sigmaSpace,borderType=4/*_CV2_BORDER_DEFAULT*/){
	var dst = ..cv2.Mat()
	var err = dll.imgproc_bilateralFilter(src.cvPtr,dst.cvPtr,d,..raw.double(sigmaColor),..raw.double(sigmaSpace),borderType)
	return dst; 
}

threshold = function(src,thresh,maxVal=255,thresholdType=0x0/*_CV2_THRESH_BINARY*/){
	var dst = ..cv2.Mat()
	var err,ret = dll.imgproc_threshold(src.cvPtr,dst.cvPtr,..raw.double(thresh),..raw.double(maxVal),thresholdType,..raw.double(0,true))
	//var err,ret = dll.imgproc_threshold(src.cvPtr,dst.cvPtr,(thresh),(maxVal),thresholdType,(0))
	return ret,dst
}

adaptiveThreshold = function(src,maxValue,adaptiveMethod=1/*_CV2_ADAPTIVE_THRESH_GAUSSIAN_C*/,thresholdType=0x0/*_CV2_THRESH_BINARY*/,blockSize,C){
	var dst = ..cv2.Mat()
	var err = dll.imgproc_adaptiveThreshold(src.cvPtr,dst.cvPtr,..raw.double(maxValue),adaptiveMethod,thresholdType,blockSize,..raw.double(C));
	return dst; 	
}

//owner.Canny = owner.canny
Canny = function(src,threshold1, threshold2, apertureSize=3, L2gradient=true){
	var dst = ..cv2.Mat()
	dll.imgproc_Canny1(src.cvPtr,dst.cvPtr,..raw.double(threshold1),..raw.double(threshold2),apertureSize,L2gradient)
	return dst; 	
}

line = function(src,from,to,scalar,thickness=1,lineType=0x10/*_CV2_LINE_AA*/,shift=0,tipLength=0.1){
    var err = dll.imgproc_line(src.cvPtr,f2i(from),f2i(to),scalar,thickness,lineType,shift)
	return src; 
}

arrowedLine = function(src,from,to,scalar,thickness=1,lineType=0x10/*_CV2_LINE_AA*/,shift=0,tipLength=0.1){
	var err = dll.imgproc_arrowedLine(src.cvPtr,from,to,scalar,thickness,lineType,shift,..raw.double(tipLength))
	return src; 
}

rectangle = function(src,from,to,scalar,thickness=1,lineType=0x10/*_CV2_LINE_AA*/,shift=0){
	var err = dll.imgproc_rectangle_InputOutputArray_Point(src.cvPtr,from,to,scalar,thickness,lineType,shift);
	return src; 
}

circle = function(src,center,radius,scalar,thickness=1,lineType=0x10/*_CV2_LINE_AA*/,shift=0){
    var err = dll.imgproc_circle(src.cvPtr,center,radius,scalar,thickness,lineType,shift)
	return src; 
}

ellipse = function(src,center,axes,angle,startAngle,endAngle,color,thickness=1,lineType=0x10/*_CV2_LINE_AA*/,shift=0){
	var err = dll.imgproc_ellipse1(src.cvPtr,center,axes,..raw.double(angle),..raw.double(startAngle),..raw.double(endAngle),color,thickness,lineType,shift)
	return src; 
}

drawMarker = function(src,position,color,markerType,markerSize=20,thickness=1,lineType=0x10/*_CV2_LINE_AA*/){
    var err = dll.imgproc_drawMarker(src.cvPtr,position,color,markerType,markerSize,thickness,lineType)
    return src; 
}

putText = function(src,text,position,font=0/*_CV2_FONT_HERSHEY_SIMPLEX*/,fontSize=1,color,thickness=1){
    if !color color = ::Scalar(255,255,255)
    if(..string.find(text,":")){
        font = font : "宋体"
    	var err = dll.imgproc_putTextZh(src.cvPtr,..string.fromto(text),f2i(position),fontSize,color,font,false,false)
    }else {
    	var err = imgproc_putText(src.cvPtr,text,f2i(position),font,..raw.double(fontSize),color,thickness,0x10/*_CV2_LINE_AA*/,0)
    }
	return src; 
}

getTextSize = function(text,fontFace=0/*_CV2_FONT_HERSHEY_SIMPLEX*/,fontScale,thickness){
    var ref = ::SIZE() 
    var err,baseLine = imgproc_getTextSize(text,fontFace,..raw.double(fontScale),thickness,..raw.int(0,true),ref)
	return ref,baseLine;
}

getStructuringElement = function(shape=0/*_CV2_MORPH_RECT*/,ksize,anchor){
	anchor = anchor : ::POINT(-1,-1)
	var ref = {pointer p};
	var err,ret = dll.imgproc_getStructuringElement(shape,ksize,anchor,ref);
	return ..cv2.Mat(ref.p); 
}

morphologyEx = function(src,op,kernel,iterations=1,anchor,borderType=0/*_CV2_BORDER_CONSTANT*/,borderValue){
    anchor = anchor : ::POINT(-1,-1)
    var dst = ..cv2.Mat()
	if(borderType==0/*_CV2_BORDER_CONSTANT*/ or !borderValue){
    	borderValue = ::Scalar(0,0,0)
    }
    var err = dll.imgproc_morphologyEx(src.cvPtr,dst.cvPtr,op,kernel.cvPtr,anchor,iterations,borderType,borderValue);
    return dst; 
}

erode = function(src,kernel,iterations=1,anchor,borderType=0/*_CV2_BORDER_CONSTANT*/,borderValue){
	anchor = anchor : ::POINT(-1,-1)
	var dst = ..cv2.Mat()
	if(borderType==0/*_CV2_BORDER_CONSTANT*/ or !borderValue){
    	borderValue = ::Scalar(0,0,0)
    } 
	var err = dll.imgproc_erode(src.cvPtr,dst.cvPtr,kernel.cvPtr,anchor,iterations,borderType,borderValue);
    return dst; 
}

dilate = function(src,kernel,iterations=1,anchor,borderType=0/*_CV2_BORDER_CONSTANT*/,borderValue){
	anchor = anchor : ::POINT(-1,-1)
	var dst = ..cv2.Mat()
	if(borderType==0/*_CV2_BORDER_CONSTANT*/ or !borderValue){
    	borderValue = ::Scalar(0,0,0)
    } 
	var err = dll.imgproc_dilate(src.cvPtr,dst.cvPtr,kernel.cvPtr,anchor,iterations,borderType,borderValue);
    return dst; 
}

distanceTransform = function(src,distanceType,maskSize){
	var dst = ..cv2.Mat();
	var err = dll.imgproc_distanceTransform(src.cvPtr,dst.cvPtr,distanceType,maskSize,null);
	return dst; 
}

findContours = function(src,mode=3/*_CV2_RETR_TREE*/,method=2/*_CV2_CHAIN_APPROX_SIMPLE*/){
    var contours = ..cv2.vector.mat()
    var hierarchy = ..cv2.Mat()
	var err = dll.imgproc_findContours1_OutputArray(src.cvPtr,contours,hierarchy.cvPtr,mode,method,::POINT())
	return src,contours,hierarchy; 
}

drawContours = function(src,contours,contourIdx=-1,color,thickness=1,lineType=0x10/*_CV2_LineTypes_LINE_AA*/){
    var cType = type.rget(contours);
    if(cType=="vectorOfMat"){
    	contours = contours.toArray()
    }
    var contoursLength = #contours
    if(contoursLength==0){
    	return ; 
    }
    color = color : (::Scalar(0,255,0))
    var err = dll.imgproc_drawContours_InputArray(src.cvPtr,{ ptr p[] = contours },contoursLength,contourIdx,color,thickness,lineType,null,0x7FFFFFFF/*_MAXLONG*/,::POINT())
	return src; 
}


/**
findContours = function(src,mode=3/*_CV2_RETR_TREE*/,method=2/*_CV2_CHAIN_APPROX_SIMPLE*/){
    //var contours = ..cv2.vector.mat()
    //var hierarchy = ..cv2.Mat()
	//var err = dll.imgproc_findContours1_OutputArray(src.cvPtr,contours,hierarchy.cvPtr,mode,method,::POINT())
	var contours = ..cv2.vector.vecPoint();
	var hierarchy = ..cv2.vector.vec4i();
	var err = dll.imgproc_findContours1_vector(src.cvPtr,contours,hierarchy,mode,method,::POINT())
	return src,contours,hierarchy; 
}

drawContours = function(src,contours,contourIdx=-1,color,thickness=1,lineType=0x10/*_CV2_LineTypes_LINE_AA*/,hierarchy=0 ){
    /*var cType = type.rget(contours);
    if(cType=="vectorOfMat"){
    	contours = contours.toArray()
    }*/
    /*
    var contoursLength = #contours
    if(contoursLength==0){
    	return ; 
    }*/
    color = color : (::Scalar(0,255,0))
    var err = dll.imgproc_drawContours_vector(src.cvPtr,contours,contours.size,contourIdx,color,thickness,lineType,hierarchy,hiearchyLength,0x7FFFFFFF/*_MAXLONG*/,::POINT())
	return src; 
}
**/
minAreaRect = function(points){
    //var mp = ..cv2.Mat(points)
    var err,ret = dll.imgproc_minAreaRect_InputArray(points.cvPtr,::RotatedRect( ::POINTF(),::POINTF(),0 ))
	//mp.release()
	return ret; 
}

boxPoints = function(box){
    var ref = {
		struct value[] = {
			length = 4;
			::POINTF();
		}
    }
    var err = dll.imgproc_boxPoints_Point2f(box,ref)
    return ref.value;
}

boundingRect = function(input){
	var t = type.rget(input)
	if(t=="mat"){
		var rect = ::RECT()
		var err = dll.imgproc_boundingRect_InputArray(input.cvPtr,rect)
		return rect; 
	}
}

convexHull = function(cnt,clockwise=1,returnPoints=0){
    var dst = ..cv2.Mat()
	var err = dll.imgproc_convexHull_InputArray(cnt.cvPtr,dst.cvPtr,clockwise,returnPoints)
	return dst; 
}


matchTemplate = function(src,temp,mode=5/*_CV2_TM_CCOEFF_NORMED*/,mask){
    var dst = ..cv2.Mat()
	var err = dll.imgproc_matchTemplate(src.cvPtr,temp.cvPtr,dst.cvPtr,mode,mask)
	return dst; 
}

filter2D = function(src,ddepth=-1,kernel,anchor,delta=0,borderType=4/*_CV2_BORDER_DEFAULT*/){
	var dst = ..cv2.Mat()
	anchor = anchor : (::POINT(-1,-1))
	var err = dll.imgproc_filter2D(src.cvPtr,dst.cvPtr,ddepth,kernel.cvPtr,anchor,..raw.double(delta),borderType)
	return dst; 
}

getRotationMatrix2D = function(center,angle,scale=1){
    var ret,p = imgproc_getRotationMatrix2D(center,..raw.double(angle),..raw.double(scale),{pointer value})
    return ..cv2.Mat(p.value);
}

warpAffine = function(src,M,sz,flags=1/*_CV2_INTER_LINEAR*/,borderMode=0/*_CV2_BORDER_CONSTANT*/,borderValue=0){
    var dst = ..cv2.Mat()
    if(!sz){
    	sz = ::SIZE(src.cols,src.rows)
    }
    if(type.rget(sz)!=type.rget(::SIZE())){
    	sz = ::SIZE(sz[1],sz[2])
    }
    if(borderValue==0){
    	borderValue = ::Scalar()
    }
	var ret = dll.imgproc_warpAffine(src.cvPtr,dst.cvPtr,M.cvPtr,sz,flags,borderMode,borderValue)
	return dst; 
}

getAffineTransform = function(src,dst){
    if(#src!=3 and #dst!=3){
    	error("POINTF数组长度只能为3")
    	return ; 
    }
    var ref = {pointer value}
	dll.imgproc_getAffineTransform1({struct s[]=src},{struct d[]=dst},ref)
	return ..cv2.Mat(ref.value); 
}

getPerspectiveTransform = function(src,dst){
    if(#src!=4 and #dst!=4){
    	error("POINTF数组长度只能为4")
    	return ; 
    }
    var ref = {pointer value}
    dll.imgproc_getPerspectiveTransform1({struct s[]=src},{struct d[]=dst},ref)
    return ..cv2.Mat(ref.value); 
}

warpPerspective = function(src,M,sz,flags=1/*_CV2_INTER_LINEAR*/,borderMode=0/*_CV2_BORDER_CONSTANT*/,borderValue=0){
    var dst = ..cv2.Mat()
    if(!sz){
    	sz = ::SIZE(src.cols,src.rows)
    }
    if(type.rget(sz)!=type.rget(::SIZE())){
    	sz = ::SIZE(sz[1],sz[2])
    }
    if(borderValue==0){
    	borderValue = ::Scalar()
    }
	var ret = dll.imgproc_warpPerspective_MisInputArray(src.cvPtr,dst.cvPtr,M.cvPtr,sz,flags,borderMode,borderValue)
	return dst;
}

HoughLines = function(src,rho=1,theta=0.017453292519943,threshold=150,srn=0,stn=0){
    var lines = ..cv2.vector.vec2f();
	var err = dll.imgproc_HoughLines(src.cvPtr,lines.ptr,..raw.double(rho),..raw.double(theta),threshold,..raw.double(srn),..raw.double(stn))
	var res = lines.toArray(); 
	lines.release()
	return res
}

HoughLinesP = function(src,rho,theta,threshold,minLineLength=0,maxLineGap=0){
    var lines = ..cv2.vector.vec4i();
	var err = dll.imgproc_HoughLinesP(src.cvPtr,lines,..raw.double(rho),..raw.double(theta),threshold,..raw.double(minLineLength),..raw.double(maxLineGap))
	var res = lines.toArray(); 
	lines.release()
	return res
}

HoughCircles = function(src,method,dp,minDist,param1,param2,minRadius,maxRadius){
	var circles = ..cv2.vector.vec3f();
	var err = dll.imgproc_HoughCircles(src.cvPtr,circles,method,..raw.double(dp),..raw.double(minDist),..raw.double(param1),..raw.double(param2),minRadius,maxRadius)
	var res = circles.toArray(); 
	circles.release()
	return res; 
}

calcHist = function(images,dims,channels,mask){
	//dll.imgproc_calcHist
}

/*}}*/

/*videoio{{*/
class VideoCapture{
	ctor( index,apiPreference=0 ){
		//dll = ..cv2._dll;
		var err,ret = dll.videoio_VideoCapture_new1({pointer p})
		this.ptr = ret.p
		var tArg = type(index)
		if(tArg==type.string && #index < 0x410/*_MAX_PATH_U8*/){
			err,ret = dll.videoio_VideoCapture_open1(this.ptr,index,apiPreference,{int value})
		}elseif(tArg==type.number){
			err,ret = dll.videoio_VideoCapture_open2(this.ptr,index,apiPreference,{int value})
		}
	};
	
	isOpened = function(){
		var err,ret = dll.videoio_VideoCapture_isOpened(this.ptr,{int op})
		return ret.op==1; 
	}
	
	read = function(){
		var dst = ..cv2.Mat()
		var err,ret = dll.videoio_VideoCapture_read_OutputArray(this.ptr,dst.cvPtr,{int value})	
		return ret.value==1,dst
	}
	
	grab = function(){
		var err,ret = dll.videoio_VideoCapture_grab(this.ptr,{int value});
		return ret.value==1; 
	}
	
	retrieve = function(flag=0){
		var dst = ..cv2.Mat();
		var err,ret = dll.videoio_VideoCapture_retrieve_OutputArray(this.ptr,dst.cvPtr,flag,{int value})
		return ret==1,dst; 
	}
	
	get = function(propId){
		var err,ret = dll.videoio_VideoCapture_get(this.ptr,propId,..raw.double(0,true))
		return ret;
	}
	
	set = function(propId,value){
		var err,ret = dll.videoio_VideoCapture_set(this.ptr,propId,..raw.int(0,true))
		return ret==1; 
	}
	
	getBackendName = function(){
		var buf = ..raw.buffer(255);
		var err = dll.videoio_VideoCapture_getBackendName(this.ptr,buf)
		return ..raw.str(buf)
	}
	
	release = function(){
		dll.videoio_VideoCapture_delete(this.ptr)
	}
}

VideoWriter_fourcc = function(c1,c2,c3,c4){
	var err,ret = dll.videoio_VideoWriter_fourcc(c1,c2,c3,c4,{int value})
	return ret.value; 
}

videoio_VideoWriter_new2 = dll.api("videoio_VideoWriter_new2","int(string filename,int fourcc, double fps,struct frameSize,int isColor,pointer value" )
videoio_VideoWriter_new3 = dll.api("videoio_VideoWriter_new3","int(string filename,int apiPreference,int fourcc, double fps,struct frameSize,int isColor,pointer value)" )

class VideoWriter{
	ctor( arg,... ){
		tArg = type(arg)
		if(tArg==type.null){
			var err,ret = dll.videoio_VideoWriter_new1({pointer value})
			this.ptr = ret.value
		}
		elseif(tArg==type.string){
			var params = {...}
			if(#params==4){
				var err,ret = videoio_VideoWriter_new2(arg,...)
				this.ptr = ret.value
			}
			elseif(#params==5){
				var err,ret = videoio_VideoWriter_new3(arg,...)
				this.ptr = ret.value
			}
		}
		
	};
	
	release = function(){
		if(this[["ptr"]]){
			dll.videoio_VideoWriter_delete(this.this[["ptr"]])
			this[["ptr"]] = null
		}
	}
}
/*}}*/


namespace text{
    
    
	
}

/*dnn{{*/
namespace dnn{
    dnn_blobFromImage= dll.api("dnn_blobFromImage","INT(pointer image,double scalefactor,struct size,struct mean,int swapRB,int crop,pointer& returnValue)")
	dnn_NMSBoxes_Rect = dll.api("dnn_NMSBoxes_Rect","INT(pointer bboxes,pointer scores,float score_threshold,float nms_threshold,pointer indices,float eta,int top_k)" )
	class readNet{
		ctor( model,cfg,backend=3/*_CV2_DNN_BACKEND_OPENCV*/,target=0/*_CV2_DNN_TARGET_CPU*/ ){
			err,ret = dll.dnn_readNet(model,cfg,"",{pointer value});
			if(!err){
				this.ptr = ret.value
				dll.dnn_Net_setPreferableBackend(this.ptr,backend);
				dll.dnn_Net_setPreferableTarget(this.ptr,target)
			}
			this.swap = true
			this.scale = 1.0/255
			//this.mean = ::Scalar(110,135,158)
			this.mean = ::Scalar(0,0,0)
			this.$config = {
				confThreshold = 0.5; //分类置信度
				nmsThreshold = 0.4; //nms置信度
			}
		};
		
		setConfig = function(config){
			..table.assign( this.$config,config)
		}
		
		setMean = function(b,g,r){
			this.mean = ::Scalar(b,g,r)
		}
		
		getUnconnectedOutLayers = function(){
			var vecInt32 = ..cv2.vector.int32();
			dll.dnn_Net_getUnconnectedOutLayers(this.ptr,vecInt32)
			return vecInt32.toArray();
		}
		
		getUnconnectedOutLayersNames = function(){
			var vecString = ..cv2.vector.string();
			dll.dnn_Net_getUnconnectedOutLayersNames(this.ptr,vecString)
			return vecString.toArray(); 
		}
		
		getLayerNames = function(){
			var vecString = ..cv2.vector.string()
			dll.dnn_Net_getLayerNames(this.ptr,vecString);
			return vecString.toArray(); 
		}
		setInputSize = function(w,h){
			if(type(w)==type.table and w[["cx"]] and w[["cy"]])
			{
				this.size = w
			}
			elseif(!!w and !!h){
				this.size = ::SIZE(w,h)
			}
		}
		
		setInputScale = function(scale){
			this.scale = scale : (1.0/255)
		}
		// bgr to rgb
		setInputSwapRB = function(swap=true){
			this.swap = swap
		}
		
		blobFromImage = function(src,scale,size){
			if scale this.setInputScale(scale)
			if size this.setInputSize(size)
			var ret,ret2,ret3 = ..cv2.dnn.dnn_blobFromImage(src,this.scale,this.size,this.mean,1,0,null)
			return ..cv2.Mat(ret2)
		}
		
		setInput = function(blob,name){
			dll.dnn_Net_setInput(this.ptr,blob,name)
		}
		
		forward = function(names){
			var mats = ..cv2.vector.mat(#names).toArray()
			var err,ret = dll.dnn_Net_forward3(this.ptr,{pointer value[]=mats},#names,{string s[]=names},#names)
			return mats;
		}
	}
	
	//dnn 模块内通用函数
	nmsBoxesRect2d = function(boxes,confidences,classIds){
		if(!#boxes){
			return; 
		}
		vConfidences = ..cv2.vector.float(confidences)
		vBoxes = ..cv2.vector.rect2d(boxes)
		vIndices = ..cv2.vector.int32();
		var err,ret = ..cv2.dnn_NMSBoxes_Rect2d(vBoxes,vConfidences,this.$config.confThreshold,this.$config.nmsThreshold,vIndices,1.0,0)
		var boxes = {}
		var scores = {}
		var ids = {}

		var boxesArray = vBoxes.toArray()
		vIndicesArray = vIndices.toArray()
		vConfidencesArray = vConfidences.toArray()
		for(i=1;vIndices.size;1){
			var idx = vIndicesArray[i];
			var rect = boxesArray[idx+1]
			..table.push( boxes,rect )
			..table.push( scores,vConfidencesArray[idx+1] )
			..table.push( ids,classIds[idx+1]+1)
		}

		//还原回0-1的浮点矩阵
		boxes = ..table.map(boxes,function(v,k,result){
			return ::RECTF(	v.left/this.size.cx,
							v.top/this.size.cy,
							v.right/this.size.cx,
							v.bottom/this.size.cy
							); 
		})
		return boxes,scores,ids; 		
	}
	
	nmsBoxes = function(boxes,confidences,confThreshold,nmsThreshold){
		if(!#boxes){
			return; 
		}
		vConfidences = ..cv2.vector.float(confidences)
		vBoxes = ..cv2.vector.rect(boxes)
		vIndices = ..cv2.vector.int32();
		var err,ret = ..cv2.dnn.dnn_NMSBoxes_Rect(vBoxes,vConfidences,confThreshold,nmsThreshold,vIndices,1.0,0)
		var res = vIndices.toArray();
		vIndices.release()
		return res;
	}
}
/*}}*/

// video
class BackgroundSubtractor{
	ctor( handle ){
		var err,ret = dll.video_Ptr_BackgroundSubtractor_get(handle,{pointer ptr})
		this.ptr = ret.ptr
	};
	
	getBackgroundImage = function(){
		var dst = ..cv2.Mat()
		err = dll.video_BackgroundSubtractor_getBackgroundImage(this.ptr,dst.cvPtr)
		return dst;
	}
	
	apply = function(image,lr=-1.0){
		var dst = ..cv2.Mat()
		err = dll.video_BackgroundSubtractor_apply(this.ptr,image.cvPtr,dst.cvPtr,..raw.double(lr))
		return dst; 
	}
}

class createBackgroundSubtractorMOG2{
	ctor( history=500,varThreshold=16.0,detectShadows=1 ){
		err,ret = dll.video_createBackgroundSubtractorMOG2(history,..raw.double(varThreshold),detectShadows,{pointer value})
		this.handle = ret.value
		if(!err){
			this = ..cv2.BackgroundSubtractor(this.handle)
		}
		..table.gc(this,"delete")
	};
	
	delete = function(){
		if(this.handle){
			dll.video_Ptr_BackgroundSubtractorMOG2_delete(this.handle)
		}
		this.handle = null
	}
}

class createBackgroundSubtractorKNN{
	ctor( history=500,varThreshold=16.0,detectShadows=1 ){
		err,ret = dll.video_createBackgroundSubtractorKNN(history,..raw.double(varThreshold),detectShadows,{pointer value})
		this.handle = ret.value
		if(!err){
			this = ..cv2.BackgroundSubtractor(this.handle)
		}
		..table.gc(this,"delete")		
	};
	
	delete = function(){
		if(this.handle){
			dll.video_Ptr_BackgroundSubtractorKNN_delete(this.handle)
		}
		this.handle = null
	}
}

//以下为第三方库，按需封装
/*WeChatQRCode{{*/
class wechat_qrcode_WeChatQRCode{
	ctor( detector_prototxt_path,detector_caffe_model_path,super_resolution_prototxt_path,super_resolution_caffe_model_path){
		var err,ret = dll.wechat_qrcode_WeChatQRCode_new(detector_prototxt_path,
		detector_caffe_model_path,
		super_resolution_prototxt_path,
		super_resolution_caffe_model_path,{pointer value})
		this.ptr = ret.value
		..table.gc( this,"release" )
	};
	
	detectAndDecode = function(img){
		var points = ..cv2.vector.mat()
		var lists = ..cv2.vector.string();
		var err = dll.wechat_qrcode_WeChatQRCode_detectAndDecode(this.ptr,img.cvPtr,points,lists);
		var mats = points.toArray();
		resPoints = ..table.map(mats,function(v,k,result){
			var minRect = ..cv2.minAreaRect(v);
			var minRectPoints = ..cv2.boxPoints(minRect)
			v.release()
			return minRectPoints;
		})
		var reLists = lists.toArray()
		points.release();
		lists.release()
		return reLists,resPoints;
	}
	
	release = function(){
		if(this[["ptr"]]){
			dll.wechat_qrcode_WeChatQRCode_delete(this.ptr);
			this[["ptr"]] = null
		}
	}
}
/*}}*/

/*barcode{{*/
class barcode{
	ctor( srPath,srModel ){
		var err,ret = dll.barcode_Barcode_new(srPath,srModel,{pointer value})
		this.ptr = ret.value
		..table.gc( this,"release" )
	};
	
	detectAndDecode = function(img){
		var points = ..cv2.vector.point()
		var lists = ..cv2.vector.string()
		var err = dll.barcode_Barcode_detectAndDecode(this.ptr,img.cvPtr,lists,points);
		var reStrs = lists.toArray(); 
		var rePs = points.toArray();
		lists.release()
		points.release()
		return reStrs,rePs
	}
	
	
	release = function(){
		if(this[["ptr"]]){
			dll.barcode_Barcode_delete(this.ptr);
			this[["ptr"]] = null
		}		
	}
}

/*}}*/

_dll = dll

namespace cv2{   
    import fsys
	import cv2.Mat
	import cv2.types
	import cv2.vector
}

/**intellisense(cv2)
imread() = !matObject.
imread(.(图片路径,载入模式) = 读取图片
imwrite(.(路径,输入图像,图片参数 可选) = 保存图片至本地路径
imdecode() = !matObject.
imdecode(.(图像字符串,载入模式) = 字符串数据转为Mat对象
imencode(.(图片扩展名,输入图像,图片参数 可选) = 将输入图像转换成流数据,返回raw.buffer字符串
imshow(.(窗口标题,显示内容) = 弹出窗口,显示图片
destroyAllWindows() = 销毁所有cv2窗体
waitKey(0) = 窗口等待键盘指令,返回键位码
waitKey(.(延时 单位毫秒) = 窗口延时等待
namedWindow(.(窗口标题,缩放类型_CV2_WINDOW) = 设置cv窗口
cvGetWindowHandle(.(窗口标题) = 根据窗口标题获取cv窗口句柄
threshold() = !matObject.
threshold(.(灰度图,起始值,最大值,阈值类型-默认THRESH_BINARY) = 图像阈值处理,返回当前阈值 处理后图像
adaptiveThreshold() = !matObject.
adaptiveThreshold(.(灰度化图像,最大灰度值,自适应阈值算法,,阈值类型,区域大小,常数 可为负数) = 自适应阈值二值化
medianBlur() = !matObject.
medianBlur(.(源数据,核大小) = 中值滤波
resize() = !matObject.
resize(.(源数据,::SIZE对象,x轴缩放倍数 缺省1.0,y轴缩放倍数 缺省1.0,插值方式) = 调整图片大小
GaussianBlur() = !matObject.
GaussianBlur(.(输入图像,::SIZE对象 高斯核大小,X方向上的高斯核标准偏差, Y方向上的高斯核标准差,边缘模式) = 高斯滤波
blur() = !matObject.
blur(.(输入图像,::SIZE对象 核大小) = 均值滤波,对于椒盐噪声的滤除效果比较好。
boxFilter() = !matObject.
boxFilter(.(输入图像,输出图像深度 默认-1 与输入图像相同,::SIZE对象 核大小,是否标准化 默认true 为false时与blur函数相同,边缘模式) = 方块滤波
bilateralFilter() = !matObject.
bilateralFilter(.(输入图像,邻域直径范围,颜色空间过滤器的sigma值,坐标空间中滤波器的sigma值,边缘模式) = 双边滤波,此函数运行较慢
cvtColor() = !matObject.
cvtColor(.(输入图像,颜色空间代码) = 转换颜色空间
Canny() = !matObject.
Canny(.(输入图像,阈值1,阈值2,Sobel算子大小 默认3,是否使用L2范数 默认true) = 边缘检测
line() = !matObject.
line(.(输入图像,起点POINT对象,终点POINT对象,颜色Scalar对象BGRA,线条宽度 默认1像素,线条类型 可选 ) = 在输入图像上画线段
arrowedLine() = !matObject.
arrowedLine(.(输入图像,起点POINT对象,终点POINT对象,颜色Scalar对象BGRA,线条宽度 默认1像素 ,线条类型 可选,箭头笔尖的长度 默认0.1 ) = 在输入图像上画箭头线段
rectangle() = !matObject.
rectangle(.(输入图像,起点POINT对象,终点POINT对象,颜色Scalar对象BGRA,线条宽度 默认1像素 为-1时填充闭合区域,线条类型 可选) = 在输入图像上画矩形
circle() = !matObject.
circle(.(输入图像,中心点坐标 POINT对象,圆半径,线条颜色 Scalar对象BGRA,线条宽度 默认1像素 为-1时填充闭合区域,线条类型 可选) = 在输入图像上画圆形
ellipse() = !matObject.
ellipse(.(输入图像,椭圆中心坐标 POINT对象,椭圆长轴短轴 SIZE对象,椭圆旋转角度以度为单位,椭圆弧的起始角度以度为单位,椭圆弧的终止角度以度为单位,线条颜色 Scalar对象BGRA,线条宽度 默认1像素 为-1时填充闭合区域,线条类型 可选) = 在输入图像上画椭圆
drawMarker() = !matObject.
drawMarker(.(输入图像,图案坐标,图案描边颜色,图案类型,图案尺寸 默认20,线条宽带 默认1像素,线条类型 可选) = 在输入图像上画图案
putText() = !matObject.
putText(.(输入图像,文字,左上角坐标,字体 可选,字体大小 可选,颜色Scalar对象BGRA 可选,字体粗细 可选) = 在输入图像上写文字,支持中文
getTextSize(.(文字,字体,字体大小,字体粗细) = 计算并返回包含指定文本的框的大小,返回::SIZE对象,距离文本最低点对应的y坐标
copyMakeBorder() = !matObject.
copyMakeBorder(.(输入图像,上边宽度,下边宽度,左边宽度,右边宽度,边界类型 可选,边框类型为0时,可设置边框颜色 默认黑色) = 扩充图像边框,返回新图像
getStructuringElement() = !matObject.
getStructuringElement(.(元素形状 默认矩形,元素大小 ::SIZE对象,锚点位置 默认中心点) = 返回指定形状和尺寸的内核矩阵 
morphologyEx() = !matObject.
morphologyEx(.(输入图像,形态学类别,结构内核,迭代次数 默认为1) = 通用形态学变换,可作用在输入图像上
erode() = !matObject.
erode(.(输入图像,结构内核,迭代次数 默认为1) = 腐蚀操作
dilate() = !matObject.
dilate(.(输入图像,结构内核,迭代次数 默认为1) = 膨胀操作
normalize() = !matObject.
normalize(.(输入图像,alpha值,beta值) = 图像归一化
bitwise_and() = !matObject.
bitwise_and(.(输入图像1,输入图像2,mask腌膜 可选) = 对图像的每一个像素值进行逻辑与操作
bitwise_or() = !matObject.
bitwise_or(.(输入图像1,输入图像2,腌膜 可选) = 对图像的每一个像素值进行逻辑或操作
bitwise_xor() = !matObject.
bitwise_xor(.(输入图像1,输入图像2,腌膜 可选) = 对图像的每一个像素值进行逻辑异或操作
bitwise_not() = !matObject.
bitwise_not(.(输入图像1,输入图像2,腌膜 可选) = 对图像的每一个像素值进行逻辑非操作
split(.(输入图像) = 将多通道矩阵分成多个单通道矩阵,返回多个Mat对象
merge() = !matObject.
merge(.(BGR通道图像数组)) = 将BGR通道图像合并
flip() = !matObject.
flip(.(输入图像,翻转码 1水平翻转 0垂直翻转 -1水平垂直翻转) = 图像翻转
rotate() = !matObject.
rotate(.(输入图像,旋转码 _CV2_ROTATE_) = 将2D图像矩阵旋转90度的倍数
subtract() = !matObject.
subtract(.(输入图像1,输入图像2,图像掩码 可选,输出图像深度 可选) = 图像减法运算
add() = !matObject.
add(.(输入图像1,输入图像2,图像掩码 可选,输出图像深度 可选) = 图像加法运算
addWeighted() = !matObject.
addWeighted(.(输入图像1,图像1权重,输入图像2,图像2权重,偏移量 缺省为0,深度 缺省-1) = 图像融合
matchTemplate() = !matObject.
matchTemplate(.(待搜索图像,模板图像,匹配模式 默认5,图像腌膜 可选) = 模板匹配
minMaxLoc(.(输入矩阵) = 返回输入矩阵的最小值,最大值,最小值索引,最大值索引
distanceTransform() = !matObject.
distanceTransform(.(输入图像,距离算法类型,距离变换腌膜大小) = 距离变换
inRange() = !matObject.
inRange(.(输入图像,包含下边界的数组或标量,包含上边界数组或标量) = 多通道阈值二值化
findContours(.(输入图像,轮廓检索模式,轮廓近似方法) = 轮廓检测
drawContours() = !matObject.
drawContours(.(输入图像,轮廓数组,绘制起点 默认-1,线条颜色,线条宽度 为-1时填充图案,线条样式) = 在输入图像上绘制轮廓
minAreaRect(.(points点集合) = 获取最小外接矩形,返回RotatedRect对象
boxPoints(.(RotatedRect对象) = 矩形四角坐标,返回4个::POINTF对象数组
boundingRect(.(输入矩阵) = 返回输入矩阵的最小边框
getTickCount() = 获取当前系统启动时间，单位毫秒
getTickFrequency() = 用于返回CPU的频率
compare() = !matObject.
compare(.(输入矩阵1,输入矩阵2,比较符_CV2_CMP) = 比较2个矩阵,返回结果矩阵,匹配像素标记为255 不匹配标记为0
hconcat() = !matObject.
hconcat( = 矩阵-图像水平拼接
hconcat(.(任意数量的同尺寸矩阵) = 矩阵-图像水平拼接
vconcat() = !matObject.
vconcat( = 矩阵-图像垂直拼接
vconcat(.(任意数量的同尺寸矩阵) = 矩阵-图像垂直拼接
setNumThreads(.(线程数 可选) = 设定OpenCV使用的线程数,默认为核心数。不要超过核心数1.5倍，过高无用
getNumThreads() = 获取当前OpenCV使用的线程数
getVersionString() = 获取当前OpenCV版本号
filter2D() = !matObject.
filter2D(.(输入图像,目标深度 默认-1,卷积核 由getStructuringElement函数构建,内核锚点 缺省为中心点,偏置值 缺省为0,外推法 可选) = 任意线性滤波器卷积\n如果锚点坐标为中心点,卷积核大小可以为偶数
getRotationMatrix2D() = !matObject.
getRotationMatrix2D(.(图像旋转的中心位置,图像旋转的角度 单位为度 正值为逆时针旋转,两个轴的比例因子 可以实现旋转过程中的图像缩放 可选 缺省为不缩放) = 生成旋转矩阵,返回一个2x3的Mat对象
getAffineTransform() = !matObject.
getAffineTransform(.(3个元素的::POINTF输入数组,3个元素的::POINTF输出数组) = 由2组3个点数组创建2x3的旋转矩阵
warpAffine() = !matObject.
warpAffine(.(输入图像,2x3的旋转矩阵,输出图像尺寸 可选 缺省时与源图相同,插值方法 可选,像素边界外推方法 可选,填充边界使用数值 可选) = 仿射变换
getPerspectiveTransform() = !matObject.
getPerspectiveTransform(.(4个元素的::POINTF输入数组,4个元素的::POINTF输出数组) = 由2组4个点数组创建2x4的透视矩阵
warpPerspective() = !matObject.
warpPerspective(.(输入图像,2x4的透视矩阵,输出图像尺寸 可选 缺省时与源图相同,插值方法 可选,像素边界外推方法 可选,填充边界使用数值 可选) = 透视变换
HoughLines(.(输入图像,扫描步长,角度步长,线段长度阈值,是否多尺度 可选,是否多尺度 可选) = 霍夫线变换
HoughLinesP(.(输入图像,扫描步长,角度步长,线段长度阈值,线段最小长度,线段最大长度) = 累计概率霍夫线变换,返回线段起始点坐标数组
HoughCircles(.(单通道图像,检测方法 默认HOUGH_GRADIENT,累加器数组大小,最小距离,边缘检测梯度值,累加器阈值,最小半径,最大半径) = 霍夫圆检测
end intellisense**/

/**intellisense()
cv2.VideoCapture() = 视频或摄像头解析对象
cv2.VideoCapture( = 创建视频解析对象
cv2.VideoCapture(.(摄像头编号或视频路径) = 创建视频解析对象,视频为非avi mpg格式时需opencv_videoio_ffmpeg453.dll
cv2.VideoCapture() = !cap.
!cap.read() = 读取视频下一帧，返回读取状态,mat对象
!cap.isOpened = 检测视频或摄像头是否打开，注意：视频有时候并不准确
!cap.get(.(_CV2_CAP_PROP_) = 获取VideoCapture对象参数
!cap.set(.(_CV2_CAP_PROP_) = 设置VideoCapture对象参数
!cap.grab() = 从视频文件或捕获设备中获取下一帧
!cap.retrieve() = 解码并返回抓取的视频帧,返回mat对象
!cap.getBackendName() = 获取API后端名称
!cap.release() = 释放VideoCapture对象,解除视频文件或摄像头占用
end intellisense**/

/**intellisense()
cv2.createBackgroundSubtractorMOG2() = MOG2动态目标检测
cv2.createBackgroundSubtractorMOG2( = 创建基于MOG2的动态目标检测对象
cv2.createBackgroundSubtractorMOG2() = !subtractor.
!subtractor.apply() = !matObject.
!subtractor.apply(.(image) = 输入图像,进行学习
!subtractor.getBackgroundImage() = 获取背景\n!matObject.

cv2.createBackgroundSubtractorKNN() = KNN动态目标检测
cv2.createBackgroundSubtractorKNN( = 创建基于KNN的动态目标检测对象
cv2.createBackgroundSubtractorKNN() = !subtractor.
!subtractor.apply() = !matObject.
!subtractor.apply(.(image) = 输入图像,进行学习
!subtractor.getBackgroundImage() = 获取背景\n!matObject.
end intellisense**/

/*intellisense()
cv2.dnn.readNet() = 基于深度学习的推理模块,支持TensorFlow Pytorch Torch Caffe DarkNet ONNX框架模型\n!dnnDetection.
cv2.dnn.readNet( = 载入权重文件,创建深度学习推理模块,支持yolo ssd等常见网络
cv2.dnn.readNet(.(权重文件,网络结构文件,推理引擎 缺省为opencv,运行载体 缺省为CPU) = 创建深度学习推理模块
!dnnDetection.getUnconnectedOutLayersNames() = 获取输出层名称,返回字符串数组
!dnnDetection.setInputSize(.(高,宽) = 设置输入大小
!dnnDetection.setInputSize(.(::SIZE对象) = 设置输入大小
!dnnDetection.setInputScale(1.0/255) = 设置缩放比例
!dnnDetection.setInputSwapRB(true) = 设置是否交换RB通道
!dnnDetection.blobFromImage(.(图像矩阵,缩放因子,输入尺寸 ::SIZE对象) = 返回一个4通道N维数组,结构为NCHW,用于神经网络输入
!dnnDetection.setConfig( 参数表 ) = @.setConfig({
	confThreshold = 0.5; //分类置信度\n
	nmsThreshold = 0.4; //nms置信度\n
})

!dnnDetection.setInput(.(blob) = 将数据输入到网络中
!dnnDetection.forward(.(输出层数组) = 前向推理,返回输出矩阵数组
!dnnDetection.setMean(.(b,g,r) = 设置各通道均值,默认为0 0 0
cv2.dnn.nmsBoxes(.(box数组,置信度数组,分类置信度,nms置信度) = 非极大值抑制算法,用于筛选出最佳锚框,返回筛选后box数组的索引数组
/*end intellisense*/


/**intellisense()
cv2.Mat() = 创建Mat矩阵对象\n!matObject.
cv2.Mat() = 创建一个空Mat矩阵
cv2.Mat(.(Mat指针) = 由Mat指针创建Mat矩阵
cv2.Mat(.(bitmap对象) = 由bitmap对象创建Mat矩阵
cv2.Mat(.(输入Mat矩阵,::RECT对象) = 从感兴趣区域(ROI)中创建Mat对象
cv2.Mat(.(行数,列数,矩阵类型_CV_,数组,步长 缺省时为自动步长) = 由数值数组创建Mat矩阵对象
end intellisense**/

/**intellisense()
!matObject() = !matObject.
!matObject(.(x,y,w,h) = 截取矩阵,返回新矩阵
end intellisense**/

/**intellisense(!matObject)
type() = 图片类型
cols = 图片列数
width = 图片宽度
dims = 维度
rows = 图片行数
height = 图片高度
channels() = 图片通道数
depth() = 图片深度
shape = 图片形状，包括高度、宽度、通道数
empty() = 判断是否图像是否为空
size() = 获取图片尺寸,返回::SIZE结构体
data = Mat对象data内存指针
datastart = 
dataend = 
datalimit = 
cvPtr = Mat对象输入输出指针,一般作为api参数被调用
elemSize = 矩阵一个元素占用的字节数
elemSize1 = 矩阵元素一个通道占用的字节数
toBitmap() = 转换为bitmap对象,返回bitmap对象
clone() = 深拷贝Mat矩阵\n!matObject.
copyTo() = !matObject.
copyTo(.(目标矩阵,附加掩膜 可选) = 将原矩阵到目标矩阵中，可以附加掩膜
ones() = !matObject.
ones(.(行数,列数,矩阵类型) = 生成所有元素为1的Mat对象
zeros() = !matObject.
zeros(.(行数,列数,矩阵类型) = 生成所有元素为0的Mat对象
eye() = !matObject.
eye(.(行数,列数,矩阵类型) = 返回一个恒等指定大小和类型矩阵
t() = 转置矩阵\n!matObject.
convertTo() = !matObject.
convertTo(.(转换类型_CV_,缩放因子,偏移因子) = 转换矩阵类型
reshape() = !matObject.
reshape(.(通道数,行数) = 重置矩阵形状\n通道数为0时,保持通道数不变\n行数为0时,保持行数不变
reserve(.(行数) = 保留指定数量的行空间,成功返回true
at(.(...) = 获取矩阵中某点的值,支持1-3维参数,返回最大10个索引的数组
row() = !matObject.
row(.(行号) = 获取矩阵中的某行矩阵对象
col() = !matObject.
col(.(行号) = 获取矩阵中的某列矩阵对象
rowRange() = !matObject.
rowRange(.(起行,止行) = 为矩阵的指定行区间创建一个矩阵头.索引从0开始,且左开右闭
colRange() = !matObject.
colRange(.(起列,止列) = 为矩阵的指定列区间创建一个矩阵头.索引从0开始,且左开右闭
diag() = 以一维数组的形式返回矩阵的对角线(或非对角线)元素,仅限方形矩阵\n!matObject.
isContinuous() = 矩阵成员是否连续
exp() = !matObject.
exp() = 返回所有元素的指数,返回新矩阵
log() = !matObject.
log() = 返回所有元素的自然对数,返回新矩阵.当源像素<=0,返回的像素上会被标记为一个大的负数
release() = 释放Mat对象,Mat占用内存较大时,需手动调用该函数
end intellisense**/

//第三方库
/**intellisense()
cv2.wechat_qrcode_WeChatQRCode() = 微信QRCode解码对象,相关模型至https://github.com/WeChatCV/opencv_3rdparty下载
cv2.wechat_qrcode_WeChatQRCode( = 创建微信QRCode解码对象
cv2.wechat_qrcode_WeChatQRCode(.(detect网络,detect权重,sr网络,sr权重) = 创建微信QRCode解码对象
cv2.wechat_qrcode_WeChatQRCode() = !QRCode.
!QRCode.detectAndDecode(.(输入图像) = 定位并解码二维码,返回 识别字符串数组,最小外接矩形四角POINT数组
!QRCode.release() = 释放微信QRCode解码对象,可自动回收

cv2.barcode() = 条形码定位与识别类库,相关模型至https://github.com/WeChatCV/opencv_3rdparty下载
cv2.barcode( = 创建条形码定位与识别对象
cv2.barcode(.(sr网络,sr权重) = 创建条形码定位与识别对象
cv2.barcode() = !BarCode.
!BarCode.detectAndDecode(.(输入图像) = 定位并解码条形码,返回 识别字符串数组,四角坐标数组
!BarCode.release() = 释放微信条形码解码对象,可自动回收
end intellisense**/