
//业层服务层,业务逻辑实现,原则这层不写sql语句
namespace svc.web.webUserSvc

import dbase.mysql.webUserDB;

string = ..string;
table = ..table;

var webUserDB = dbase.mysql.webUserDB;

getPageList = function(tabPage){
	var mainSql = "select * from web_user";
	var where = "1=1 ";
	var txt = tabPage.search;
	if( #txt ){
		txt = string.replace(txt,"'","''");
		//where += "and (ShopName+Address) like '%" ++ txt ++ "%'";//mssql
		//where += "and CONCAT(SystemUserName,' ',LoginAccount) like '%" ++ txt ++ "%'";//mysql
	}
	//where = webUserDB.formatSqlParameters(where);
	var orderby = "UserId desc";
	return webUserDB.getPageList(tabPage, mainSql, orderby, where);
}

update = function(tpost,userId){
	tpost.UpdateTime = ..common.getNowTime();
	var ok,err = webUserDB.update(tpost,"userId=@userId",{userId=userId});
	return ok,err; 
}

getInfo = function(userId){
	var tab,err = webUserDB.getFirstList("userId=@userId",{userId=userId});
	if(!tab) return null,err;
	if( !table.count(tab) ) return null,"无记录";
	return tab;
}

//注册保存
regSave = function(tpost){
	var tab,err = webUserDB.getFirstList("email=@email and nickname=@nickname", {email=tpost.email;nickname=tpost.username});
	if(!tab) return false,err;	
	if(table.count(tab) > 0) return false,"邮箱或呢称已被注册";
 
	var tUser = {};
	tUser.Email = tpost.email;
	tUser.NickName = tpost.username;
	tUser.Password = ..common.encryptPwdMd5(tpost.pass);
	tUser.EmailStatus = "0";
	tUser.MobileStatus = "0";
	tUser.UserStatus = "1";
	tUser.Points = 100;
	tUser.PostCount = 0;
	tUser.ReplyCount = 0;
	tUser.IsAdmin = "0";
	tUser.RegisterTime = ..common.getNowTime();
	tUser.RegisterIP = tpost.regip;
	tUser.AvatarUrl = "/ui/res/images/avatar/" ++ ..math.random(0, 50) ++ ".jpg";
	tUser.UserId,err = webUserDB.getAutoId();
	if(!tUser.UserId) return false,err;
	var ok,err = webUserDB.insert(tUser);
	return ok,err; 
}

//用户登录
checkLogin = function(email,pass){
	if(!#email || !#pass) return null,"无效信息";
	
	pass = ..common.encryptPwdMd5(pass);	
	var tabInfo,err = webUserDB.getFirstList("email=@email and Password=@pass",{email=email;pass=pass})
	if( !tabInfo ) return false,err;
	 
	if( table.count(tabInfo) ){		
		if(tabInfo.UserStatus != "1")	return false,"您的帐号已被禁用"; 
		
		var nowTime = ..common.getNowTime();
		var tabUserLogin = {};//登录信息
		tabUserLogin.UserId = tabInfo.UserId;
		tabUserLogin.Email = tabInfo.Email;
		tabUserLogin.NickName = tabInfo.NickName;
		tabUserLogin.IsAdmin = (tabInfo.IsAdmin == "1");
		tabUserLogin.LoginTime = nowTime;

		..carl.session.set("tabUserLogin",tabUserLogin);//保存session

		var tab = {}
		tab.LoginTime = nowTime;
		tab.OnlineTime = nowTime;
		tab.LoginCount = ..common.toNumber(tabInfo.LoginCount) + 1;
		tab.LoginIP = ..request.environ.REMOTE_ADDR;
		webUserDB.update(tab, "UserId=@UserId", {UserId=tabInfo.UserId});
		
		return true;
	}else {
		return false,"登录失败,邮箱或密码错误!"; 
	}
}

//修改密码
editPwd = function(nowpass,pass,useId){
	nowpass = ..common.encryptPwdMd5(nowpass);
	var tab = getInfo(useId) : {};
	if(tab.Password != nowpass) return false,"当前密码不正确"; 
	
	var tpost = {}
	tpost.password = ..common.encryptPwdMd5(pass);
	return update(tpost,userId);
}
