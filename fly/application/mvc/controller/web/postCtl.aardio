
import string.html;
import svc.web.webCategorySvc;
import svc.web.webPostSvc;

namespace mvc.controller.web

post = class {
	ctor( actionName, controllerPath ){
		this.base = ..mvc.controller.web.base();//继承基类控制器
		actionName = string.lower(actionName);
		var tab = {"add";"edit";"save";"upload"};
		if(table.find(tab,actionName)){//需要登录
			if( !this.base.isLogin ) this.base.reLogin();
		}
	};
	
	index = function(){
		call(this.page,owner);
	}

	add = function(){
		var tabParam = {};
		tabParam.title = "发表问题";
		tabParam.add = true;
		tabParam.tabCategory = webCategorySvc.getCategoryTree();
		owner.view.loadcode("web.post.add",tabParam);
	}
	
	edit = function(id){
		var postId = ..common.toNumber(id);
		var tabParam = {};		
		tabParam.title = "编辑问题";
		tabParam.edit = true;

		var limit,msg = webPostSvc.getLimit(postId, this.base.userId, this.base.user.IsAmdin);//修改权限
		if(limit > 0) tabParam.detail = webPostSvc.getInfo(postId);//详情
		tabParam.limit = limit;
		tabParam.msg = msg;
		
		tabParam.tabCategory = webCategorySvc.getCategoryTree();
		owner.view.loadcode("web.post.add",tabParam);
	}
	
	detail = function(id){
		var postId = ..common.toNumber(id);
		var tabParam = {};
		tabParam.detail = webPostSvc.getPostDetail(postId);
		owner.view.loadcode("web.post.detail",tabParam);
	}
	
	unsolved = function(page){
		call(this.page,owner,page,"unsolved");
	}
	
	solved = function(page){
		call(this.page,owner,page,"solved");
	}
	
	wonderful = function(page){
		call(this.page,owner,page,"wonderful");
	}	
	
	page = function(page,status){
		page = ..common.toNumber(page) : 1;
		var tabParam = {};
		select(status) {
			case "unsolved" {
				tabParam.title = "未结帖";
			}
			case "solved" {
				tabParam.title = "已采纳";
			}
			case "wonderful" {
				tabParam.title = "精帖";
			}
		}
		tabParam.status = status;
		tabParam.action = status : "page";
		tabParam.list = webPostSvc.getWebPageList(page,..config.webconfig.pageSize:30,status);
		owner.view.loadcode("web.post.index",tabParam);
	}
	
	save = function(title,content,categoryId,vercode,id){
		if(#title<4 || #title>100)
			return false,"标题在4-100个字符之间"; 
			
		if(..common.toNumber(categoryId) <= 0)
			return false,"分类错误";
		
		if(!#content) return false,"内容不能为空";
		
		var postId = ..common.toNumber(id);
		if(postId < 0) return false,"非法ID";
		 
		var ok,err = ..common.code.check(vercode);//校验验证码
		if(!ok) return false,err; 
		
		ok,err = webPostSvc.userSavePost(title,content,categoryId,postId, this.base.userId, this.base.user.IsAmdin);
		if(!ok) return false,err;
		return true,"发布成功",..mvcapp.urlAction("detail&id=" ++ ok,"web.post")
	}
	
	upload = function(){
		var imageUrl,err = ..common.uploadFile();//获取上传后的图片
		if(imageUrl){
			var t = {".jpg";".gif";".jpeg";".png";"bmp"}
			var ext = string.lower(io.splitpath(imageUrl).ext);
			if(!table.find(t,ext)){
				io.remove(imageUrl);
				return false,"不支持图片格式"; 
			}
		}
		if(imageUrl) imageUrl,err = ..common.imageThumbnail(imageUrl,168,168);//生成缩略图
		if(!imageUrl) return false,err;
		var tpost = {}
		tpost.AvatarUrl = imageUrl;
		var ok,err = webUserSvc.update(tpost,this.base.userId);
		if(!ok) return false,err;
		return true
	}
}

namespace post{
	webCategorySvc = ..svc.web.webCategorySvc;
	webPostSvc = ..svc.web.webPostSvc;
}
