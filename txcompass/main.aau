import win.ui;
import process;
import fsys;
import fsys.dlg;
loadcode("\res\logger.aau")() //记录日志到文件
loadcode("\res\readflow.aau")() //下载罗盘流水
loadcode("\res\handleflow.aau")() //处理流水内容
/*DSG{{*/
var mainForm = ..win.form(text="腾讯罗盘流水";right=431;bottom=255;border="dialog frame";max=false;parent=...)
mainForm.add(
btn_debug={cls="button";text="调试";left=336;top=32;right=368;bottom=56;color=0;font=LOGFONT( name='微软雅黑' );z=25};
btn_decode={cls="button";text="解码流水";left=304;top=168;right=400;bottom=224;color=0;font=LOGFONT( name='微软雅黑';h=-19 );z=1};
btn_file={cls="button";text="F";left=280;top=32;right=296;bottom=56;color=0;font=LOGFONT( name='微软雅黑';h=-16 );z=27};
btn_load={cls="button";text="加载罗盘";left=304;top=80;right=400;bottom=136;color=0;font=LOGFONT( name='微软雅黑';h=-19 );z=2};
btn_login={cls="button";text="登录";left=368;top=32;right=400;bottom=56;color=0;font=LOGFONT( name='微软雅黑' );z=24};
btn_save={cls="button";text="S";left=280;top=80;right=296;bottom=104;color=0;font=LOGFONT( name='微软雅黑';h=-16 );z=12};
btn_web={cls="button";text="网页";left=304;top=32;right=336;bottom=56;color=0;font=LOGFONT( name='微软雅黑' );z=26};
cb_begintime={cls="combobox";left=56;top=80;right=120;bottom=104;color=0;edge=1;font=LOGFONT( name='黑体';h=-16 );items={"00:00";"02:00";"04:00";"06:00";"08:00";"10:00";"12:00";"14:00";"16:00";"18:00";"20:00";"22:00"};mode="dropdownlist";z=10};
cb_endtime={cls="combobox";left=56;top=112;right=120;bottom=136;color=0;edge=1;font=LOGFONT( name='黑体';h=-16 );items={"01:59";"03:59";"05:59";"07:59";"09:59";"11:59";"13:59";"15:59";"17:59";"19:59";"21:59";"23:59"};mode="dropdownlist";z=11};
cb_filter={cls="combobox";left=32;top=200;right=112;bottom=224;color=0;edge=1;font=LOGFONT( name='黑体';h=-16 );items={"#操作号";"@关键字"};mode="dropdownlist";z=17};
cb_game={cls="combobox";left=176;top=32;right=280;bottom=56;color=0;edge=1;font=LOGFONT( name='黑体';h=-16 );items={};mode="dropdownlist";z=22};
cb_plat={cls="combobox";left=32;top=32;right=120;bottom=56;color=0;edge=1;font=LOGFONT( name='黑体';h=-16 );items={};mode="dropdownlist";z=8};
dtp_date={cls="datetimepick";left=176;top=112;right=296;bottom=136;color=0;edge=1;font=LOGFONT( name='黑体';h=-16 );z=9};
ed_column={cls="edit";left=120;top=168;right=296;bottom=192;align="center";autohscroll=false;autovscroll=false;color=0;edge=1;font=LOGFONT( name='微软雅黑';h=-14 );hidesel=1;z=13};
ed_filter={cls="edit";left=120;top=200;right=296;bottom=224;align="center";autohscroll=false;autovscroll=false;color=0;edge=1;font=LOGFONT( name='微软雅黑';h=-16 );hidesel=1;z=14};
ed_user={cls="edit";left=176;top=80;right=280;bottom=104;align="center";color=0;edge=1;font=LOGFONT( name='微软雅黑';h=-16 );hidesel=1;num=1;z=3};
gb_head={cls="groupbox";left=16;top=8;right=416;bottom=72;color=0;font=LOGFONT( name='Cambria' );z=7};
gb_main={cls="groupbox";left=16;top=8;right=416;bottom=240;color=0;font=LOGFONT( name='Cambria' );z=4};
gb_mleft={cls="groupbox";left=16;top=64;right=128;bottom=152;color=0;font=LOGFONT( name='Cambria' );z=5};
gb_mright={cls="groupbox";left=128;top=64;right=416;bottom=152;color=0;font=LOGFONT( name='Cambria' );z=6};
st_begintime={cls="static";text="始";left=32;top=80;right=56;bottom=104;align="center";center=1;color=0;font=LOGFONT( name='微软雅黑';h=-16 );nWrap=1;transparent=1;z=21};
st_column={cls="static";text="数据显示列";left=32;top=168;right=112;bottom=191;align="center";center=1;color=0;font=LOGFONT( name='黑体';h=-16 );nWrap=1;transparent=1;z=16};
st_date={cls="static";text="日期";left=136;top=112;right=176;bottom=136;align="center";center=1;color=0;font=LOGFONT( name='微软雅黑';h=-16 );nWrap=1;transparent=1;z=20};
st_endtime={cls="static";text="终";left=32;top=112;right=56;bottom=136;align="center";center=1;color=0;font=LOGFONT( name='微软雅黑';h=-16 );nWrap=1;transparent=1;z=23};
st_game={cls="static";text="游戏";left=136;top=32;right=176;bottom=56;align="center";center=1;color=0;font=LOGFONT( name='微软雅黑';h=-16 );nWrap=1;transparent=1;z=19};
st_outline={cls="static";left=32;top=144;right=400;bottom=160;align="center";border=1;center=1;color=0;disabled=1;font=LOGFONT( name='宋体' );z=15};
st_user={cls="static";text="用户";left=136;top=80;right=176;bottom=104;align="center";center=1;color=0;font=LOGFONT( name='微软雅黑';h=-16 );nWrap=1;transparent=1;z=18}
)
/*}}*/

var cfgfolder = string.replace(io._exepath,"\.\w*$","") //建立同名目录
if not( fsys.isDir(cfgfolder) ){ fsys.createDir(cfgfolder) }

/*初始化{{*/
var mixer = loadcode("\res\mixfunc.aau") //返回基础数据

var inifile = mixer("initial_config") //程序配置
var commtbl = mixer("obtain_commtable") //公共表
mixer("generate_column") //生成默认的显示列

global.biscuit = inifile.read("share","biscuit") : "cookie:" //请求头

//默认焦点在用户ID框
mainForm.ed_user.setFocus()
//默认过滤方式
mainForm.cb_filter.selIndex = 1
//所在平台
mainForm.cb_plat.items = inifile.readKeys("domain")
mainForm.cb_plat.selIndex = 1
//应用名称
mainForm.cb_game.items = inifile.readKeys("appset")
mainForm.cb_game.selIndex = 1
//查询时间段（默认全天）
mainForm.cb_begintime.selIndex = 1
mainForm.cb_endtime.selIndex = #mainForm.cb_endtime.items
//程序控件配置（上次退出时的设置）
import config;
mainForm.bindConfig( config.widget,{
	cb_plat = "selIndex";
	cb_game = "selIndex";
	cb_filter = "selIndex";
} );

mainForm.dtp_date.setFormat("yyyy'/'MM'/'dd") //日期格式，防止受系统时间格式影响
/*}}*/

//应用ID被改变
mainForm.cb_game.oncommand = function(id,event){
	if(event == 0x1/*_CBN_SELCHANGE*/){
		resumeInitialAndChange()
	}
}

//打开罗盘网页
mainForm.btn_web.oncommand = function(id,event){
	process.execute("http://compass.qq.com/dlogs?appid=" ++ commtbl.appid)
}

//退出程序时记录COOKIES
mainForm.wndproc = function(hwnd,message,wParam,lParam){ 
	select(message) {
		case 0x10/*_WM_CLOSE*/ {
			inifile.write("share","biscuit",global.biscuit)
		}
	}
}

//QQ登录窗口
qqLogin = function(){
	var qq,wb = mainForm.loadForm("\res\qqlogin.aau", mainForm/*父窗口*/); 
	qq.login(15004102/*,true*/) //认证ID，取自原登录页面
	var qqlogin = qq.doModal(mainForm)
}

//点击「登录」按钮
mainForm.btn_login.oncommand = function(id,event){
	qqLogin()
}

//硬盘上的流水文件路径
mainForm.btn_file.oncommand = function(id,event){
	var select_flow = fsys.dlg.open("所有文件(*.*)|*.*|")
	if(!select_flow){ return } //没有选择文件，返回
	mainForm.st_outline.text = select_flow
	//mainForm.ed_user.text = io.splitpath(select_flow).file
	mainForm.ed_user.text = "[1-9]+"
}

//保存流水到硬盘文件
mainForm.btn_save.oncommand = function(id,event){
	if( string.startWith(mainForm.st_outline.text,"[罗盘]:") ){
		var year_month_day = mainForm.dtp_date.time
		year_month_day.format = "%y%m%d"
		
		var save_path = fsys.dlg.save("默认文件类型(csv)|*.csv|所有文件类型(*.*)|*.*|",
						"保存原始流水到文件",,,,mainForm.ed_user.text ++ "_" ++ tostring(year_month_day))
		
		if( #save_path ){string.save( save_path,commtbl.flow_raw )}
	} else {
		win.msgbox("请先加载罗盘流水！","错误")
	}
}

//点击『加载罗盘』按钮
mainForm.btn_load.oncommand = function(id,event){
	if(string.trim(mainForm.ed_user.text) == ""){
		return ; //没有输入用户UID则返回
	}
	mainForm.btn_load.disabled = true; mainForm.btn_load.text = "加载ING" //避免重复点击

	var err_msgbox = ""
	if(mainForm.ed_user.text != tonumber(mainForm.ed_user.text)){
		err_msgbox += '用户UID仅能为数字！\n'
	}
	if(tonumber(mainForm.cb_begintime.selText) > tonumber(mainForm.cb_endtime.selText)){
		err_msgbox += '开始时间大于结束时间！\n'
	}
	
	if(#err_msgbox){
		win.msgbox(err_msgbox)
	} else {
		var year_month_day = mainForm.dtp_date.time
		year_month_day.format = "%Y-%m-%d"
		
		qqstat, commtbl.flow_raw = readFlow({ //加载流水到内存变量
			biscuit = global.biscuit;
			appid = commtbl.appid; //罗盘应用ID
			domain = inifile.read("domain",mainForm.cb_plat.selText,"1"); //应用所在平台，默认「QQ空间」
			date = tostring(year_month_day); //查询日期
			tm_begin = string.replace(mainForm.cb_begintime.selText ++ ":00", "\:", "%3A"); //开始时间
			tm_end = string.replace(mainForm.cb_endtime.selText ++ ":59", "\:", "%3A"); //结束时间
			user = tostring(mainForm.ed_user.text) //用户
		})
		
		if not(qqstat){
			qqLogin() //需要登录能够加载罗盘权限的QQ
		} else {
			mainForm.st_outline.text = "[罗盘]:" ++ tostring(year_month_day) ++ ":[" ++ mainForm.ed_user.text ++ "]"
		}
	}
	
	mainForm.btn_load.disabled = false; mainForm.btn_load.text = "加载罗盘" //启用按钮
}

//*点击『流水解码』按钮{{*/
mainForm.btn_decode.oncommand = function(id,event){
	mainForm.btn_decode.disabled = true; mainForm.btn_decode.text = "解码ING" //避免重复点击
	
	var flow_output = "" //保存流水解码后的文件路径
	mainForm.st_outline.text = string.trim(mainForm.st_outline.text)
	if(#mainForm.st_outline.text){
		if(string.startWith(mainForm.st_outline.text,"[罗盘]:")){
			flow_output = fsys.getTempDir() ++ "flow." ++ commtbl.appid ++ ".ada"
			/*{ 流水已加载到内存变量中，无需再读取 }*/
		} elseif (io.exist( mainForm.st_outline.text )) {
			flow_output = tostring(mainForm.st_outline.text) ++ ".txt" //保存到同级目录下的txt文件中
			commtbl.flow_raw = string.load(mainForm.st_outline.text) //载入硬盘上的流水文件
		}
	} else {
		mainForm.btn_decode.disabled = false; mainForm.btn_decode.text = "解码流水" //启用按钮
		win.msgbox("没有流水数据，无法解析！","错误")
		return "ERR_NO_FLOW_CONTENT"; //跳过当前函数的剩余代码
	}
	
	commtbl.flow_result = handleFlow({ //解码流水的函数
		flow_raw = commtbl.flow_raw; //原始流水内容
		user = tostring(mainForm.ed_user.text); //用户ID
		column = tostring(mainForm.ed_column.text); //过滤列
		filter = tostring(mainForm.ed_filter.text); //过滤模板
		filter_type = tostring(mainForm.cb_filter.selText); //过滤方式
	})
	
	if( log2file(commtbl.flow_result,flow_output) ){ //保存成功后直接打开
		process.execute(flow_output)
	}
	
	mainForm.btn_decode.disabled = false; mainForm.btn_decode.text = "解码流水" //启用按钮
}

//调试
mainForm.btn_debug.oncommand = function(id,event){
	win.invoke(
		function( ... ){ import console; console.varDump( ... ); console.pause(true); },
		commtbl.appid, global.biscuit, commtbl.flow_raw
	)
}

//标题中显示APPID、默认显示列
resumeInitialAndChange = function(){ //当APPID改变时也会触发这个函数
	mainForm.text = "罗盘【" ++ mainForm.cb_game.selText ++ "】"
	commtbl.appid = tostring( inifile.read("appset",mainForm.cb_game.selText,"19089") )
	mainForm.ed_column.text = inifile.read("column",mainForm.cb_game.selText,"5,9,14,16,18,19,20,21,22,29")
}
resumeInitialAndChange()

//预加载流水文件
if( _CMDLINE ){
	mainForm.st_outline.text = string.replace(_CMDLINE,'"','')
	mainForm.ed_user.text = io.splitpath(mainForm.st_outline.text).file
}

//显示程序界面
mainForm.show() 
win.loopMessage(); 
